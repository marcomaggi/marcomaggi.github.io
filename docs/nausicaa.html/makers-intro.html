<html lang="en">
<head>
<title>makers intro - Nausicaa for Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Nausicaa for Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="makers.html#makers" title="makers">
<link rel="next" href="makers-api.html#makers-api" title="makers api">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d4 of Nausicaa/Scheme, a set of
Scheme libraries defining a slightly modified R6RS Scheme language
and augmenting the features of the base and standard R6RS libraries.

The project home page of Nausicaa is at:

             `http://marcomaggi.github.com/nausicaa.html'


development of Nausicaa takes place at:

               `http://github.com/marcomaggi/nausicaa/'


Copyright (C) 2008, 2009, 2010 by Marco Maggi.

Copyright (C) 1996, 1997, 2000, 2001, 2002, 2003, 2004, 2005
Free Software Foundation.

Copyright (C) 1996, 1999-2005 Dorai Sitaram.

Copyright (C) 1998 Oleg Kiselyov.

Copyright (C) 1998, 1999, 2000 Olin Shivers.

Copyright (C) 1999 John David Stone.

Copyright (C) 1999, 2002 Marc Feeley.

Copyright (C) 2001, 2009 Danny Dube'

Copyright (C) 2002 Dr. Mirko Luedde.

Copyright (C) 2002, 2003, 2005, 2006 Sebastian Egner.

Copyright (C) 2003 Ray Dillinger.

Copyright (C) 2003 Taylor Campbell.

Copyright (C) 2005 Jens Axel Soegaard.

Copyright (C) 2005-2009 Alex Shinn.

Copyright (C) 2008 Taro Minowa (Higepon).

Copyright (C) 2005-2008 Dominique Boucher.

Copyright (C) 2004, 2005 Tony Garnock-Jones

Copyright (C) 2005 LShift Ltd.

Copyright (C) 2007, 2008 Philip L. Bewig.

Copyright (C) 2000 Will Fitzgerald.

Copyright (C) 2000 Neodesic Corporation.

Copyright (C) 2000-2006 Joachim Henke.

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with no Invariant Sections being ``GNU Free
     Documentation License'' and ``GNU General Public License'', no
     Front--Cover Texts, and no Back--Cover Texts.  A copy of the
     license is included in the section entitled ``GNU Free
     Documentation License''.

This document embeds an unofficial assemblage of several documents
reformatted in Texinfo; the reformatting author and maintainer is
Marco Maggi <marco.maggi-ipsu@poste.it>.  See the appendix ``Credits'' for
the list of original documents and their authors.  See also the
`README' and `CREDITS' files for additional attributions.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="makers-intro"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="makers-api.html#makers-api">makers api</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="makers.html#makers">makers</a>
<hr>
</div>

<h3 class="section">19.1 Introduction to maker macros</h3>

<p>It happens to define a function or macro accepting a number of arguments
greater than three:

<pre class="example">     (define (the-func a b c d e f g h i)
       ...)
</pre>
   <p class="noindent">in these cases it can be difficult to remember the order of the
arguments; it can also be desirable to define default values for some or
all of the arguments, so that at the call site some arguments can be
omitted in the source code.  The <code>(nausicaa makers)</code> library
implements a solution to this problem.

   <p>The following examples show the expansion of a simple maker with neither
fixed nor variable arguments, only optional clauses arguments:

<pre class="example">     (import (rnrs) (nausicaa language makers))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker doit
       list ((:alpha     1)
             (:beta      2)
             (:gamma     3)))
     
     (doit)                  ==&gt; (list 1 2 3)
     
     (doit (:alpha   10))    ==&gt; (list 10 2 3)
     (doit (:beta    20))    ==&gt; (list 1 20 3)
     (doit (:gamma   30))    ==&gt; (list 1 2 30)
     
     (doit (:alpha   10)
           (:beta    20))    ==&gt; (list 10 20 3)
     
     (doit (:alpha   10)
           (:gamma   30))    ==&gt; (list 10 2 30)
     
     (doit (:gamma   30)
           (:beta    20))    ==&gt; (list 1 20 30)
     
     (doit (:alpha   10)
           (:beta    20)
           (:gamma   30))    ==&gt; (list 10 20 30)
     
     (let ((b 7))
       (doit (:beta  (+ 6 (* 2 b)))
             (:alpha (+ 2 8)))
       ==&gt; (list (+ 2 8)
                 (+ 6 (* 2 b))
                 3)
       #f)
     
     (doit (:alpha 10 20 30))
     ==&gt; (list 1 (list 10 20 30) 3)
</pre>
   <p class="noindent">notice the last example: when multiple values are used in an argument's
clause, they are enclosed in a <code>list</code> form; the following examples
show the expansion of a maker with both fixed and variable arguments,
plus optional clauses arguments:

<pre class="example">     (import (rnrs) (nausicaa language makers))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker (doit a b)
       (list #\a #\b)
       ((:alpha  1)
        (:beta   2)
        (:gamma  3)))
     
     
     (doit #\p #\q)          ==&gt; (list #\a #\b #\p #\q 1 2 3)
     
     (doit #\p #\q
           (:alpha 10))      ==&gt; (list #\a #\b #\p #\q 10 2 3)
     
     (doit #\p #\q
           (:beta 20))       ==&gt; (list #\a #\b #\p #\q 1 20 3)
     
     (doit #\p #\q
           (:gamma 30))      ==&gt; (list #\a #\b #\p #\q 1 2 30)
     
     (doit #\p #\q
           (:alpha   10)
           (:beta    20))    ==&gt; (list #\a #\b #\p #\q 10 20 3)
     
     (doit #\p #\q
           (:alpha   10)
           (:gamma   30))    ==&gt; (list #\a #\b #\p #\q 10 2 30)
     
     (doit #\p #\q
           (:gamma   30)
           (:beta    20))    ==&gt; (list #\a #\b #\p #\q 1 20 30)
</pre>
   <p>Each default value can be any expression and it is evaluated every time
the maker is used:

<pre class="example">     (import (rnrs) (nausicaa language makers))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define g
       (let ((counter 0))
         (lambda ()
           (set! counter (+ 1 counter))
           counter)))
     
     (define default2 (- (/ 9 3) 1))
     
     (define-maker doit
       list ((:alpha     1)
             (:beta      default2)
             (:gamma     (g))))
     
     (doit)                  &rArr; ( 1  2  1)
     (doit (:alpha 10))      &rArr; (10  2  2)
     (doit (:beta  20))      &rArr; ( 1 20  3)
     (doit (:gamma 30))      &rArr; ( 1  2 30)
</pre>
   <p>A maker invocation can expand itself into another macro use; this allows
us to detect whether an optional argument was used or not:

<pre class="example">     (import (rnrs)
       (nausicaa language makers)
       (nausicaa language sentinel))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker doit
       subdoit
       ((:alpha  1)
        (:beta   2)
        (:gamma  sentinel)))
     
     (define-syntax subdoit
       (lambda (stx)
         (syntax-case stx ()
           ((_ ?alpha ?beta ?gamma)
            (and (identifier? #'?gamma)
                 (free-identifier=? #'?gamma #'sentinel))
            #'(list ?alpha ?beta 3))
           ((_ ?alpha ?beta ?gamma)
            #'(list ?alpha ?beta ?gamma))
           )))
     
     (doit)                  &rArr; ( 1  2  3)
     (doit (:alpha 10))      &rArr; (10  2  3)
     (doit (:beta  20))      &rArr; ( 1 20  3)
     (doit (:gamma 30))      &rArr; ( 1  2 30)
</pre>
   <p class="noindent">and also to &ldquo;unpack&rdquo; multiple values used in the same argument clause:

<pre class="example">     (import (rnrs)
       (nausicaa language makers)
       (nausicaa language sentinel))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker doit
       subdoit ((:alpha  1)
                (:beta   2)
                (:gamma  3)))
     
     (define-syntax subdoit
       (lambda (stx)
         (syntax-case stx (list)
           ((_ ?alpha (list ?beta0 ...) ?gamma)
            #'(list ?alpha ?beta0 ... ?gamma))
           ((_ ?alpha ?beta ?gamma)
            #'(list ?alpha ?beta ?gamma))
           )))
     
     (check
         (doit (:alpha 10)
               (:beta #\a #\b #\c)
               (:gamma 30))
       =&gt; '(10 #\a #\b #\c 30))
</pre>
   <p>We can define a maker in the body of a library and export it; we just
have to remember to export the auxiliary syntaxes, too:

<pre class="example">     (library (the-lib)
       (export doit alpha beta gamma)
       (import (rnrs) (nausicaa language makers))
     
       (define-auxiliary-syntax alpha beta gamma)
       (define-maker doit
         list ((alpha     1)
               (beta      2)
               (gamma     3)))
       )
     
     (library (exec)
       (export)
       (import (rnrs)
         (prefix (the-lib) lib.))
     
       (lib.doit)                  ==&gt; (list 1 2 3)
       (lib.doit (lib.alpha   10)) ==&gt; (list 10 2 3)
       (lib.doit (lib.beta    20)) ==&gt; (list 1 20 3)
       (lib.doit (lib.gamma   30)) ==&gt; (list 1 2 30)
       )
</pre>
   <p>A number of options can be specified to customise the parsing of
clauses.  We can specify if a clause is mandatory or optional:

<pre class="example">     (import (rnrs) (nausicaa language makers))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker doit
       list ((:alpha     1 (mandatory))
             (:beta      2 (optional))
             (:gamma     3)))
     
     (doit (:alpha 10))              ==&gt; (list 10 2 3)
     (doit (:beta  20))              error--&gt; missing clause ":alpha"
</pre>
   <p class="noindent">the default is for all the clauses to be optional.  We can specify that
a clause must be used along one or more other clauses:

<pre class="example">     (import (rnrs) (nausicaa language makers))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker doit
       list ((:alpha     1 (with :beta :gamma))
             (:beta      2)
             (:gamma     3)))
     
     (doit (:alpha 10)
           (:beta  20)
           (:gamma 30))              ==&gt; (list 10 20 30)
     
     (doit (:beta 20))               ==&gt; (list 1 20 3)
     
     (doit (:alpha 10)
           (:gamma 30))              error--&gt; missing clause ":beta"
</pre>
   <p class="noindent">or we can specify that a clause must not be used along one or more other
clauses:

<pre class="example">     (import (rnrs) (nausicaa language makers))
     
     (define-auxiliary-syntax :alpha :beta :gamma)
     
     (define-maker doit
       list ((:alpha     1 (without :beta :gamma))
             (:beta      2)
             (:gamma     3)))
     
     (doit (:alpha 10)
           (:beta  20)
           (:gamma 30))              error--&gt; invalid clauses mix
     
     (doit (:beta 20))               ==&gt; (list 1 20 3)
</pre>
   <!-- page -->
   </body></html>

