<html lang="en">
<head>
<title>streams examples pipeline - Nausicaa for Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Nausicaa for Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="streams-examples.html#streams-examples" title="streams examples">
<link rel="prev" href="streams-examples-generators.html#streams-examples-generators" title="streams examples generators">
<link rel="next" href="streams-examples-persistent.html#streams-examples-persistent" title="streams examples persistent">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d6 of Nausicaa/Scheme, a set of
Scheme libraries defining a slightly modified R6RS Scheme language
and augmenting the features of the base and standard R6RS libraries.

The project home page of Nausicaa is at:

             `http://marcomaggi.github.com/nausicaa.html'


development of Nausicaa takes place at:

               `http://github.com/marcomaggi/nausicaa/'


Copyright (C) 2008, 2009, 2010 by Marco Maggi.

Copyright (C) 1996, 1997, 2000, 2001, 2002, 2003, 2004, 2005
Free Software Foundation.

Copyright (C) 1996, 1999-2005 Dorai Sitaram.

Copyright (C) 1998 Oleg Kiselyov.

Copyright (C) 1998, 1999, 2000 Olin Shivers.

Copyright (C) 1999 John David Stone.

Copyright (C) 1999, 2002 Marc Feeley.

Copyright (C) 2001, 2009 Danny Dube'

Copyright (C) 2002 Dr. Mirko Luedde.

Copyright (C) 2002, 2003, 2005, 2006 Sebastian Egner.

Copyright (C) 2003 Ray Dillinger.

Copyright (C) 2003 Taylor Campbell.

Copyright (C) 2005 Jens Axel Soegaard.

Copyright (C) 2005-2009 Alex Shinn.

Copyright (C) 2008 Taro Minowa (Higepon).

Copyright (C) 2005-2008 Dominique Boucher.

Copyright (C) 2004, 2005 Tony Garnock-Jones

Copyright (C) 2005 LShift Ltd.

Copyright (C) 2007, 2008 Philip L. Bewig.

Copyright (C) 2000 Will Fitzgerald.

Copyright (C) 2000 Neodesic Corporation.

Copyright (C) 2000-2006 Joachim Henke.

Copyright (C) 2004-2009 Neil Van Dyke.

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with no Invariant Sections being ``GNU Free
     Documentation License'' and ``GNU General Public License'', no
     Front--Cover Texts, and no Back--Cover Texts.  A copy of the
     license is included in the section entitled ``GNU Free
     Documentation License''.

This document embeds an unofficial assemblage of several documents
reformatted in Texinfo; the reformatting author and maintainer is
Marco Maggi <marco.maggi-ipsu@poste.it>.  See the appendix ``Credits'' for
the list of original documents and their authors.  See also the
`README' and `CREDITS' files for additional attributions.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="streams-examples-pipeline"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="streams-examples-persistent.html#streams-examples-persistent">streams examples persistent</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="streams-examples-generators.html#streams-examples-generators">streams examples generators</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="streams-examples.html#streams-examples">streams examples</a>
<hr>
</div>

<h4 class="subsection">40.14.3 A pipeline of procedures</h4>

<p>Another way in which streams promote modularity is enabling the use of
many small procedures that are easily composed into larger programs, in
the style of Unix pipelines, where streams are important because they
allow a large dataset to be processed one item at a time.  Bird and
Wadler provide the example of a text formatter.  Their example uses
right&ndash;folds:

<pre class="example">     (define (stream-fold-right f base strm)
       (if (stream-null? strm)
           base
         (f (stream-car strm)
            (stream-fold-right f base
              (stream-cdr strm)))))
     
     (define (stream-fold-right-one f strm)
       (stream-match strm
         [(x) x]
         [(x . xs)
          (f x (stream-fold-right-one f xs))]))
</pre>
   <p>Bird and Wadler define text as a stream of characters, and develop a
standard package for operating on text, which they derive mathematically
(this assumes the line&ndash;separator character is a single
<code>#\newline</code>):

<pre class="example">     (define (breakon a)
       (stream-lambda (x xss)
         (if (equal? a x)
             (stream-append (stream (stream)) xss)
           (stream-append
             (stream (stream-append
                 (stream x) (stream-car xss)))
             (stream-cdr xss)))))
     
     (define-stream (lines strm)
       (stream-fold-right
         (breakon #\newline)
         (stream (stream))
         strm))
     
     (define-stream (words strm)
       (stream-filter stream-pair?
         (stream-fold-right
           (breakon #\space)
           (stream (stream))
           strm)))
     
     (define-stream (paras strm)
       (stream-filter stream-pair?
         (stream-fold-right
           (breakon stream-null)
           (stream (stream))
           strm)))
     
     (define (insert a)
       (stream-lambda (xs ys)
         (stream-append xs (stream a) ys)))
     
     (define unlines
       (lsec stream-fold-right-one
         (insert #\newline)))
     
     (define unwords
       (lsec stream-fold-right-one
         (insert #\space)))
     
     (define unparas
       (lsec stream-fold-right-one
         (insert stream-null)))
</pre>
   <p>These versatile procedures can be composed to count words, lines and
paragraphs; the <code>normalize</code> procedure squeezes out multiple spaces
and blank lines:

<pre class="example">     (define countlines
       (compose stream-length lines))
     
     (define countwords
       (compose stream-length
                stream-concat
                (lsec stream-map words)
                lines))
     
     (define countparas
       (compose stream-length paras lines))
     
     (define parse
       (compose (lsec stream-map
                  (lsec stream-map words))
                paras
                lines))
     
     (define unparse
       (compose unlines
                unparas
                (lsec stream-map
                  (lsec stream-map unwords))))
     
     (define normalize (compose unparse parse))
</pre>
   <p>More useful than normalization is text&ndash;filling, which packs as many
words onto each line as will fit.

<pre class="example">     (define (greedy m ws)
       (- (stream-length
            (stream-take-while (rsec &lt;= m)
              (stream-scan
                (lambda (n word)
                  (+ n (stream-length word) 1))
                -1
                ws))) 1))
     
     (define-stream (fill m ws)
       (if (stream-null? ws)
           stream-null
         (let* ([n (greedy m ws)]
                [fstline (stream-take n ws)]
                [rstwrds (stream-drop n ws)])
           (stream-append
             (stream fstline)
             (fill m rstwrds)))))
     
     (define linewords
       (compose stream-concat
                (lsec stream-map words)))
     
     (define textparas
       (compose (lsec stream-map linewords)
                paras
                lines))
     
     (define (filltext m strm)
       (unparse (stream-map (lsec fill m) (textparas strm))))
</pre>
   <p>To display filename in lines of <var>n</var> characters, say:

<pre class="example">     (stream-for-each display
       (filltext n (file-&gt;stream filename)))
</pre>
   <p>Though each operator performs only a single task, they can be composed
powerfully and expressively.  The alternative is to build a single
monolithic procedure for each task, which would be harder and involve
repetitive code.  Streams ensure procedures are called as needed.

<!-- page -->
   </body></html>

