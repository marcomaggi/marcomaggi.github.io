<html lang="en">
<head>
<title>csv lexer full - Nausicaa for Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Nausicaa for Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="csv-lexer.html#csv-lexer" title="csv lexer">
<link rel="prev" href="csv-lexer-unquoted_002ddata.html#csv-lexer-unquoted_002ddata" title="csv lexer unquoted-data">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d5 of Nausicaa/Scheme, a set of
Scheme libraries defining a slightly modified R6RS Scheme language
and augmenting the features of the base and standard R6RS libraries.

The project home page of Nausicaa is at:

             `http://marcomaggi.github.com/nausicaa.html'


development of Nausicaa takes place at:

               `http://github.com/marcomaggi/nausicaa/'


Copyright (C) 2008, 2009, 2010 by Marco Maggi.

Copyright (C) 1996, 1997, 2000, 2001, 2002, 2003, 2004, 2005
Free Software Foundation.

Copyright (C) 1996, 1999-2005 Dorai Sitaram.

Copyright (C) 1998 Oleg Kiselyov.

Copyright (C) 1998, 1999, 2000 Olin Shivers.

Copyright (C) 1999 John David Stone.

Copyright (C) 1999, 2002 Marc Feeley.

Copyright (C) 2001, 2009 Danny Dube'

Copyright (C) 2002 Dr. Mirko Luedde.

Copyright (C) 2002, 2003, 2005, 2006 Sebastian Egner.

Copyright (C) 2003 Ray Dillinger.

Copyright (C) 2003 Taylor Campbell.

Copyright (C) 2005 Jens Axel Soegaard.

Copyright (C) 2005-2009 Alex Shinn.

Copyright (C) 2008 Taro Minowa (Higepon).

Copyright (C) 2005-2008 Dominique Boucher.

Copyright (C) 2004, 2005 Tony Garnock-Jones

Copyright (C) 2005 LShift Ltd.

Copyright (C) 2007, 2008 Philip L. Bewig.

Copyright (C) 2000 Will Fitzgerald.

Copyright (C) 2000 Neodesic Corporation.

Copyright (C) 2000-2006 Joachim Henke.

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with no Invariant Sections being ``GNU Free
     Documentation License'' and ``GNU General Public License'', no
     Front--Cover Texts, and no Back--Cover Texts.  A copy of the
     license is included in the section entitled ``GNU Free
     Documentation License''.

This document embeds an unofficial assemblage of several documents
reformatted in Texinfo; the reformatting author and maintainer is
Marco Maggi <marco.maggi-ipsu@poste.it>.  See the appendix ``Credits'' for
the list of original documents and their authors.  See also the
`README' and `CREDITS' files for additional attributions.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="csv-lexer-full"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="csv-lexer-unquoted_002ddata.html#csv-lexer-unquoted_002ddata">csv lexer unquoted-data</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="csv-lexer.html#csv-lexer">csv lexer</a>
<hr>
</div>

<h4 class="subsection">62.1.3 Full parsing</h4>

<p>Here we see how the high level parsers are implemented.  The following
function makes use of both the string and unquoted&ndash;data tables to
convert a <acronym>CSV</acronym> file into a list of lists, with each sublist
representing a record.  The function <code>csv-&gt;list/comma</code> does no
check for the number of fields on each line.

<pre class="smallexample">     (define-keywords :port :counters)
     
     (define csv-&gt;list/comma
       (case-lambda
        ((port)
         (csv-&gt;list/comma port (lambda (field) field)))
     
        ((port swirl-field)
         (let* ((IS            (lex.make-IS (port: port)))
                (string-lexer  (lex.make-lexer
                                 csv-strings-table IS))
                (data-lexer    (lex.make-lexer
                                 csv-unquoted-data-table/comma IS))
                (result        '())
                (record        '()))
           (let-values (((port field) (open-string-output-port)))
     
             (define (%add-token-to-field token)
               (write-char token port))
     
             (define (%add-string-to-field)
               (%add-token-to-field #\")
               (do ((token (string-lexer) (string-lexer)))
                   ((not token)
                    (%add-token-to-field #\"))
                 (%add-token-to-field token)))
     
             (define (%add-field-to-record)
               (set! record (cons (swirl-field (field)) record)))
     
             (define (%add-record-to-result)
               (set! result (cons (reverse record) result))
               (set! record '()))
     
             (do ((token (data-lexer) (data-lexer)))
                 ((not token)
                  (%add-field-to-record)
                  (reverse (cons (reverse record) result)))
               (case token
     
                 ((eol)
                  (%add-field-to-record)
                  (%add-record-to-result))
     
                 ((field)
                  (%add-field-to-record))
     
                 ((string)
                  (%add-string-to-field))
     
                 (else
                  (%add-token-to-field token)))))))))
</pre>
   <p>Here some examples:

<pre class="example">     (import (rnrs)
       (nausicaa csv)
       (nausicaa strings))
     
     ; Notice how spaces around commas are preserved.
     (csv-&gt;list/comma
        (open-string-input-port "alpha, beta, delta
     one, two, three"))
     &rArr; '(("alpha" " beta" " delta")
          ("one" " two" " three"))
     
     ; Trim the spaces.
     (csv-&gt;list/comma
       (open-string-input-port "alpha, beta, delta
     one, two, three")
       (lambda (field)
         (string-trim-both field #\space)))
     &rArr; '(("alpha" "beta" "delta")
          ("one" "two" "three"))
</pre>
   <p>The following is a variant that allows to specify, via the
<var>separators</var> argument, custom characters as field separators.

<pre class="smallexample">     (define-keywords :port :counters)
     
     (define csv-&gt;list
       (case-lambda
        ((port separators)
         (csv-&gt;list port separators (lambda (field) field)))
     
        ((port separators swirl-field)
         (let* ((IS            (lex.make-IS (port: port)))
                (string-lexer  (lex.make-lexer
                                 csv-strings-table IS))
                (data-lexer    (lex.make-lexer
                                 csv-unquoted-data-table/comma IS))
                (result        '())
                (record        '()))
           (let-values (((port field) (open-string-output-port)))
     
             (define (%add-token-to-field token)
               (write-char token port))
     
             (define (%add-string-to-field)
               (%add-token-to-field #\")
               (do ((token (string-lexer) (string-lexer)))
                   ((not token)
                    (%add-token-to-field #\"))
                 (%add-token-to-field token)))
     
             (define (%add-field-to-record)
               (set! record (cons (swirl-field (field)) record)))
     
             (define (%add-record-to-result)
               (set! result (cons (reverse record) result))
               (set! record '()))
     
             (do ((token (data-lexer) (data-lexer)))
                 ((not token)
                  (%add-field-to-record)
                  (reverse (cons (reverse record) result)))
               (cond
     
                 ((eq? 'eol token)
                  (%add-field-to-record)
                  (%add-record-to-result))
     
                 ((memv token separators)
                  (%add-field-to-record))
     
                 ((eq? 'string token)
                  (%add-string-to-field))
     
                 (else
                  (%add-token-to-field token)))))))))
</pre>
   <!-- page -->
   </body></html>

