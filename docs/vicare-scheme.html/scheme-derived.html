<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- This document describes version 0.4d1 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit and 64-bit
processors.  Vicare is pronounced the etruscan way.

The package, including its documentation, is distributed under the terms
of the GNU General Public License (GPL) and can be downloaded
from:

https://bitbucket.org/marcomaggi/vicare-scheme/downloads

the home page of the project is at:

http://marcomaggi.github.io/vicare.html

development takes place at:

http://github.com/marcomaggi/vicare/

and, as a backup, at:

https://bitbucket.org/marcomaggi/vicare-scheme/

Copyright (C) 2010-2017 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

Copyright (C) Michael Sperber, R. Kent Dybvig, Matthew Flatt and Anton Van Straaten.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors, see the "History" appendix for details.

The documentation of IrRegex is Copyright (C) 2005-2012 Alex
Shinn.  All rights reserved.

The documentation of Pregexp is Copyright (C) 1999-2005 Dorai
Sitaram.  All rights reserved.

The documentation of the library (vicare formations) is derived
from the documentation of Guile.  Copyright (C) 1996-2005,
2009-2013 Free Software Foundation.

This program is free software: you can redistribute it and/or modify it
under the terms of the GNU General Public License version 3 as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see
http://www.gnu.org/licenses/.

Trademarks used herein are the property of their respective owners. -->
<!-- Created by GNU Texinfo 6.3, http://www.gnu.org/software/texinfo/ -->
<head>
<title>Vicare Scheme: scheme derived</title>

<meta name="description" content="Vicare Scheme: scheme derived">
<meta name="keywords" content="Vicare Scheme: scheme derived">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<link href="index.html#Top" rel="start" title="Top">
<link href="concept-index.html#concept-index" rel="index" title="concept index">
<link href="scheme.html#scheme" rel="up" title="scheme">
<link href="baselib.html#baselib" rel="next" title="baselib">
<link href="scheme-expansion.html#scheme-expansion" rel="prev" title="scheme expansion">
<style type="text/css">
<!--
a.summary-letter {text-decoration: none}
blockquote.indentedblock {margin-right: 0em}
blockquote.smallindentedblock {margin-right: 0em; font-size: smaller}
blockquote.smallquotation {font-size: smaller}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
div.lisp {margin-left: 3.2em}
div.smalldisplay {margin-left: 3.2em}
div.smallexample {margin-left: 3.2em}
div.smalllisp {margin-left: 3.2em}
kbd {font-style: oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
pre.smalldisplay {font-family: inherit; font-size: smaller}
pre.smallexample {font-size: smaller}
pre.smallformat {font-family: inherit; font-size: smaller}
pre.smalllisp {font-size: smaller}
span.nolinebreak {white-space: nowrap}
span.roman {font-family: initial; font-weight: normal}
span.sansserif {font-family: sans-serif; font-weight: normal}
ul.no-bullet {list-style: none}
-->
</style>


</head>

<body lang="en">
<a name="scheme-derived"></a>
<div class="header">
<p>
Previous: <a href="scheme-expansion.html#scheme-expansion" accesskey="p" rel="prev">scheme expansion</a>, Up: <a href="scheme.html#scheme" accesskey="u" rel="up">scheme</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<a name="Sample-definitions-for-derived-forms"></a>
<h3 class="section">3.11 Sample definitions for derived forms</h3>



<p>This appendix contains sample definitions for some of the keywords
described in this report in terms of simpler forms:
</p>

<a name="cond"></a>
<h4 class="subsubheading"><code>cond</code></h4>

<p>The <code>cond</code> keyword could be defined in terms of <code>if</code>,
<code>let</code> and <code>begin</code> using <code>syntax-rules</code> as follows:
</p>
<div class="example">
<pre class="example">(define-syntax cond
  (syntax-rules (else =&gt;)
    ((cond (else result1 result2 ...))
     (begin result1 result2 ...))
    ((cond (test =&gt; result))
     (let ((temp test))
       (if temp (result temp))))
    ((cond (test =&gt; result) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           (result temp)
           (cond clause1 clause2 ...))))
    ((cond (test)) test)
    ((cond (test) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           temp
           (cond clause1 clause2 ...))))
    ((cond (test result1 result2 ...))
     (if test (begin result1 result2 ...)))
    ((cond (test result1 result2 ...)
           clause1 clause2 ...)
     (if test
         (begin result1 result2 ...)
         (cond clause1 clause2 ...)))))
</pre></div>


<a name="case"></a>
<h4 class="subsubheading"><code>case</code></h4>

<p>The <code>case</code> keyword could be defined in terms of <code>let</code>,
<code>cond</code>, and <code>memv</code> using <code>syntax-rules</code> as follows:
</p>
<div class="example">
<pre class="example">(define-syntax case
  (syntax-rules (else)
    ((case expr0
       ((key ...) res1 res2 ...)
       ...
       (else else-res1 else-res2 ...))
     (let ((tmp expr0))
       (cond
         ((memv tmp '(key ...)) res1 res2 ...)
         ...
         (else else-res1 else-res2 ...))))
    ((case expr0
       ((keya ...) res1a res2a ...)
       ((keyb ...) res1b res2b ...)
       ...)
     (let ((tmp expr0))
       (cond
         ((memv tmp '(keya ...)) res1a res2a ...)
         ((memv tmp '(keyb ...)) res1b res2b ...)
         ...)))))
</pre></div>


<a name="let_002a"></a>
<h4 class="subsubheading"><code>let*</code></h4>

<p>The <code>let*</code> keyword could be defined in terms of <code>let</code> using
<code>syntax-rules</code> as follows:
</p>
<div class="example">
<pre class="example">(define-syntax let*
  (syntax-rules ()
    ((let* () body1 body2 ...)
     (let () body1 body2 ...))
    ((let* ((name1 expr1) (name2 expr2) ...)
       body1 body2 ...)
     (let ((name1 expr1))
       (let* ((name2 expr2) ...)
         body1 body2 ...)))))
</pre></div>


<a name="letrec"></a>
<h4 class="subsubheading"><code>letrec</code></h4>

<p>The <code>letrec</code> keyword could be defined approximately in terms of
<code>let</code> and <code>set!</code> using <code>syntax-rules</code>, using a helper to
generate the temporary variables needed to hold the values before the
assignments are made, as follows:
</p>
<div class="example">
<pre class="example">(define-syntax letrec
  (syntax-rules ()
    ((letrec () body1 body2 ...)
     (let () body1 body2 ...))
    ((letrec ((var init) ...) body1 body2 ...)
     (letrec-helper
       (var ...)
       ()
       ((var init) ...)
       body1 body2 ...))))

(define-syntax letrec-helper
  (syntax-rules ()
    ((letrec-helper
       ()
       (temp ...)
       ((var init) ...)
       body1 body2 ...)
     (let ((var &lt;undefined&gt;) ...)
       (let ((temp init) ...)
         (set! var temp)
         ...)
       (let () body1 body2 ...)))
    ((letrec-helper
       (x y ...)
       (temp ...)
       ((var init) ...)
       body1 body2 ...)
     (letrec-helper
       (y ...)
       (newtemp temp ...)
       ((var init) ...)
       body1 body2 ...))))
</pre></div>

<p>The syntax <code>&lt;undefined&gt;</code> represents an expression that returns
something that, when stored in a location, causes an exception with
condition type <code>&amp;assertion</code> to be raised if an attempt to read from
or write to the location occurs before the assignments generated by the
<code>letrec</code> transformation take place.  (No such expression is defined
in Scheme.)
</p>

<a name="letrec_002a"></a>
<h4 class="subsubheading"><code>letrec*</code></h4>

<p>The <code>letrec*</code> keyword could be defined approximately in terms of
<code>let</code> and <code>set!</code> using <code>syntax-rules</code> as follows:
</p>
<div class="example">
<pre class="example">(define-syntax letrec*
  (syntax-rules ()
    ((letrec* ((var1 init1) ...) body1 body2 ...)
     (let ((var1 &lt;undefined&gt;) ...)
       (set! var1 init1)
       ...
       (let () body1 body2 ...)))))
</pre></div>

<p>The syntax <code>&lt;undefined&gt;</code> is as in the definition of <code>letrec</code>
above.
</p>

<a name="let_002dvalues"></a>
<h4 class="subsubheading"><code>let-values</code></h4>

<p>The following definition of <code>let-values</code> using <code>syntax-rules</code>
employs a pair of helpers to create temporary names for the formals.
</p>
<div class="example">
<pre class="example">(define-syntax let-values
  (syntax-rules ()
    ((let-values (binding ...) body1 body2 ...)
     (let-values-helper1
       ()
       (binding ...)
       body1 body2 ...))))

(define-syntax let-values-helper1
  ;; map over the bindings
  (syntax-rules ()
    ((let-values
       ((id temp) ...)
       ()
       body1 body2 ...)
     (let ((id temp) ...) body1 body2 ...))
    ((let-values
       assocs
       ((formals1 expr1) (formals2 expr2) ...)
       body1 body2 ...)
     (let-values-helper2
       formals1
       ()
       expr1
       assocs
       ((formals2 expr2) ...)
       body1 body2 ...))))

(define-syntax let-values-helper2
  ;; create temporaries for the formals
  (syntax-rules ()
    ((let-values-helper2
       ()
       temp-formals
       expr1
       assocs
       bindings
       body1 body2 ...)
     (call-with-values
       (lambda () expr1)
       (lambda temp-formals
         (let-values-helper1
           assocs
           bindings
           body1 body2 ...))))
    ((let-values-helper2
       (first . rest)
       (temp ...)
       expr1
       (assoc ...)
       bindings
       body1 body2 ...)
     (let-values-helper2
       rest
       (temp ... newtemp)
       expr1
       (assoc ... (first newtemp))
       bindings
       body1 body2 ...))
    ((let-values-helper2
       rest-formal
       (temp ...)
       expr1
       (assoc ...)
       bindings
       body1 body2 ...)
     (call-with-values
       (lambda () expr1)
       (lambda (temp ... . newtemp)
         (let-values-helper1
           (assoc ... (rest-formal newtemp))
           bindings
           body1 body2 ...))))))
</pre></div>


<a name="let_002a_002dvalues"></a>
<h4 class="subsubheading"><code>let*-values</code></h4>

<p>The following macro defines <code>let*-values</code> in terms of <code>let</code>
and <code>let-values</code> using <code>syntax-rules</code>:
</p>
<div class="example">
<pre class="example">(define-syntax let*-values
  (syntax-rules ()
    ((let*-values () body1 body2 ...)
     (let () body1 body2 ...))
    ((let*-values (binding1 binding2 ...)
       body1 body2 ...)
     (let-values (binding1)
       (let*-values (binding2 ...)
         body1 body2 ...)))))
</pre></div>



<a name="let"></a>
<h4 class="subsubheading"><code>let</code></h4>

<p>The <code>let</code> keyword could be defined in terms of <code>lambda</code> and
<code>letrec</code> using <code>syntax-rules</code> as follows:
</p>
<div class="example">
<pre class="example">(define-syntax <code>let</code>
  (syntax-rules ()
    ((let ((name val) ...) body1 body2 ...)
     ((lambda (name ...) body1 body2 ...)
      val ...))
    ((let tag ((name val) ...) body1 body2 ...)
     ((letrec ((tag (lambda (name ...)
                      body1 body2 ...)))
        tag)
      val ...))))
</pre></div>

<hr>
<div class="header">
<p>
Previous: <a href="scheme-expansion.html#scheme-expansion" accesskey="p" rel="prev">scheme expansion</a>, Up: <a href="scheme.html#scheme" accesskey="u" rel="up">scheme</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
