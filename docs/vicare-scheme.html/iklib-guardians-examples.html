<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- This document describes version 0.4d1 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit and 64-bit
processors.  Vicare is pronounced the etruscan way.

The package, including its documentation, is distributed under the terms
of the GNU General Public License (GPL) and can be downloaded
from:

https://bitbucket.org/marcomaggi/vicare-scheme/downloads

the home page of the project is at:

http://marcomaggi.github.io/vicare.html

development takes place at:

http://github.com/marcomaggi/vicare/

and, as a backup, at:

https://bitbucket.org/marcomaggi/vicare-scheme/

Copyright (C) 2010-2017 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

Copyright (C) Michael Sperber, R. Kent Dybvig, Matthew Flatt and Anton Van Straaten.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors, see the "History" appendix for details.

The documentation of IrRegex is Copyright (C) 2005-2012 Alex
Shinn.  All rights reserved.

The documentation of Pregexp is Copyright (C) 1999-2005 Dorai
Sitaram.  All rights reserved.

The documentation of the library (vicare formations) is derived
from the documentation of Guile.  Copyright (C) 1996-2005,
2009-2013 Free Software Foundation.

This program is free software: you can redistribute it and/or modify it
under the terms of the GNU General Public License version 3 as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see
http://www.gnu.org/licenses/.

Trademarks used herein are the property of their respective owners. -->
<!-- Created by GNU Texinfo 6.3, http://www.gnu.org/software/texinfo/ -->
<head>
<title>Vicare Scheme: iklib guardians examples</title>

<meta name="description" content="Vicare Scheme: iklib guardians examples">
<meta name="keywords" content="Vicare Scheme: iklib guardians examples">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<link href="index.html#Top" rel="start" title="Top">
<link href="concept-index.html#concept-index" rel="index" title="concept index">
<link href="iklib-guardians.html#iklib-guardians" rel="up" title="iklib guardians">
<link href="iklib-guardians-api.html#iklib-guardians-api" rel="next" title="iklib guardians api">
<link href="iklib-guardians.html#iklib-guardians" rel="prev" title="iklib guardians">
<style type="text/css">
<!--
a.summary-letter {text-decoration: none}
blockquote.indentedblock {margin-right: 0em}
blockquote.smallindentedblock {margin-right: 0em; font-size: smaller}
blockquote.smallquotation {font-size: smaller}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
div.lisp {margin-left: 3.2em}
div.smalldisplay {margin-left: 3.2em}
div.smallexample {margin-left: 3.2em}
div.smalllisp {margin-left: 3.2em}
kbd {font-style: oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
pre.smalldisplay {font-family: inherit; font-size: smaller}
pre.smallexample {font-size: smaller}
pre.smallformat {font-family: inherit; font-size: smaller}
pre.smalllisp {font-size: smaller}
span.nolinebreak {white-space: nowrap}
span.roman {font-family: initial; font-weight: normal}
span.sansserif {font-family: sans-serif; font-weight: normal}
ul.no-bullet {list-style: none}
-->
</style>


</head>

<body lang="en">
<a name="iklib-guardians-examples"></a>
<div class="header">
<p>
Next: <a href="iklib-guardians-api.html#iklib-guardians-api" accesskey="n" rel="next">iklib guardians api</a>, Up: <a href="iklib-guardians.html#iklib-guardians" accesskey="u" rel="up">iklib guardians</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<a name="Usage-examples-for-guardians"></a>
<h4 class="subsection">6.42.1 Usage examples for guardians</h4>


<p>Let&rsquo;s say that we use the Vicare <acronym>FFI</acronym> to handle some memory
block (<a href="ffi.html#ffi">ffi</a> for details on the <acronym>FFI</acronym>); memory blocks allocated
with <code>malloc</code> are not released by the Vicare garbage
collector: we have to explicitly apply <code>free</code> to the pointer value
referencing them.
</p>
<p>If we use the blocks synchronously with the evaluation of forms, we do
(assuming we do not use continuations, so this use of
<code>dynamic-wind</code> is fine):
</p>
<div class="example">
<pre class="example">(import (vicare)
  (vicare ffi))

(define (do-something-with . args)
  (display args)
  (newline))

(let ((p        #f)
      (size     4096))
  (dynamic-wind
      (lambda ()
        (set! p (malloc size))
        (unless p (error #f &quot;memory allocation&quot;)))
      (lambda ()
        (do-something-with p))
      (lambda ()
        (free p))))
</pre></div>

<p>and we can define a syntax for it:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (vicare ffi))

(define (do-something-with . args)
  (display args)
  (newline))

(define-syntax with-block
  (syntax-rules ()
    ((_ ?pointer ?size ?body ...)
     (let ((?pointer    #f))
       (dynamic-wind
           (lambda ()
             (set! ?pointer (malloc ?size))
             (unless ?pointer (error #f &quot;memory allocation&quot;)))
           (lambda ()
             ?body ...)
           (lambda ()
             (free ?pointer)))))))

(with-block p 2048
  (do-something-with p))

(with-block p 4096
  (do-something-with p))

(with-block p 8192
  (do-something-with p))
</pre></div>

<p>If we need the block in an event driven program: we will probably need
to use it asynchronously with the evaluation of forms.  For example, we
store the pointer value that references a block in a thunk (a closure
that takes no arguments):
</p>
<div class="example">
<pre class="example">(import (vicare)
  (vicare ffi))

(define *event-source* '())

(define (enqueue-event event)
  (set! *event-source*
        (reverse (cons event (reverse *event-source*)))))

(define (pop-event)
  (if (null? *event-source*)
      #f
    (let ((event (car *event-source*)))
      (set! *event-source* (cdr *event-source*))
      event)))

;; Usage:

(define (do-something-with . args)
  (display args)
  (newline))

(let ((p (malloc 4096)))
  (unless p (error #f &quot;memory allocation&quot;))
  (enqueue-event (lambda ()
                   (do-something-with p)))
  (enqueue-event (lambda ()
                   (do-something-with 123)))
  (enqueue-event (lambda ()
                   (do-something-with p))))

(do ((event (pop-event) (pop-event)))
    ((not event))
  (event))
</pre></div>

<p>once the thunks have been evaluated, the pointer value is garbage
collected, but the allocated memory block becomes leaked memory.  We
need a way to be notified of the pointer value garbage collection, so
that we can apply <code>free</code> to it; this is the job for guardians.
</p>
<p>The following script shows the usage of a guardian to free a memory
block:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (vicare ffi))

(define g (make-guardian))

(let ((a (malloc (expt 2 20))))
  (unless p (error #f &quot;memory allocation&quot;))
  (g a)
  (printf &quot;value ~s~%&quot; a))

(printf &quot;value from guardian ~s~%&quot; (g))

;; This triggers a garbage collection.
(collect)

(let ((p (g)))
  (printf &quot;value from guardian ~s~%&quot; p)
  (free p))
</pre></div>

<p>In an event driven program what we have to do is to register the pointer
into the guardian, and then periodically enqueue as event a call to the
guardian:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (vicare ffi))

;; Event source handling.

(define *event-source* '())

(define (enqueue-event event)
  (set! *event-source*
        (reverse (cons event (reverse *event-source*)))))

(define (pop-event)
  (if (null? *event-source*)
      #f
    (let ((event (car *event-source*)))
      (set! *event-source* (cdr *event-source*))
      event)))

;; Block guardian.

(define block-guardian (make-guardian))

(define (run-block-guardian)
  (do ((p (block-guardian) (block-guardian)))
      ((not p))
    (printf &quot;collecting ~s~%&quot; p)
    (free p)))

;; Application follows.

(define (do-something-with . args)
  (display args)
  (newline))

(let ((p (malloc (expt 2 20))))
  (unless p (error #f &quot;memory allocation&quot;))
  (block-guardian p)
  (enqueue-event (lambda () (do-something-with p)))
  (enqueue-event (lambda () (do-something-with 123)))
  (enqueue-event (lambda () (do-something-with p)))
  (enqueue-event (lambda () (do-something-with 456))))

(do ((event (pop-event) (pop-event))
     (i 1 (+ i 1)))
    ((= i 20))
  (when event
    (event))
  (when (= 0 (modulo i 10))
    (collect)
    (enqueue-event run-block-guardian)))
</pre></div>

<hr>
<div class="header">
<p>
Next: <a href="iklib-guardians-api.html#iklib-guardians-api" accesskey="n" rel="next">iklib guardians api</a>, Up: <a href="iklib-guardians.html#iklib-guardians" accesskey="u" rel="up">iklib guardians</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
