<html lang="en">
<head>
<title>srfi err5rs records examples - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="srfi-err5rs-records.html#srfi-err5rs-records" title="srfi err5rs records">
<link rel="prev" href="srfi-err5rs-records-spec.html#srfi-err5rs-records-spec" title="srfi err5rs records spec">
<link rel="next" href="srfi-err5rs-records-ack.html#srfi-err5rs-records-ack" title="srfi err5rs records ack">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d20 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package is distributed under the terms of the GNU General Public
License (GPL) and can be downloaded from:

               `http://code.google.com/p/vicare-scheme/'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="srfi-err5rs-records-examples"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="srfi-err5rs-records-ack.html#srfi-err5rs-records-ack">srfi err5rs records ack</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="srfi-err5rs-records-spec.html#srfi-err5rs-records-spec">srfi err5rs records spec</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="srfi-err5rs-records.html#srfi-err5rs-records">srfi err5rs records</a>
<hr>
</div>

<h4 class="subsection">10.32.4 Examples</h4>

<p><acronym>R6RS</acronym> library section 6.3 includes two extended examples that provide
a nice comparison of the <acronym>R6RS</acronym> and <acronym>ERR5RS</acronym> record systems,
especially since these two examples were designed to highlight the use
of <acronym>R6RS</acronym> record&ndash;constructor descriptors in combination with
inheritance.

<!--  -->
<h5 class="subsubheading">Example 1</h5>

<p>Using <acronym>ERR5RS</acronym> records, the first example becomes:

<pre class="example">     (define rtd1
       (make-rtd 'rtd1 '#((immutable x1) (immutable x2))))
     
     (define rtd2
       (make-rtd 'rtd2 '#((immutable x3) (immutable x4)) rtd1))
     
     (define rtd3
       (make-rtd 'rtd3 '#((immutable x5) (immutable x6)) rtd2))
     
     (define protocol1
       (lambda (p)
         (lambda (a b c)
           (p (+ a b) (+ b c)))))
     
     (define protocol2
       (lambda (n)
         (lambda (a b c d e f)
           (let ((p (n a b c)))
             (p (+ d e) (+ e f))))))
     
     (define protocol3
       (lambda (n)
         (lambda (a b c d e f g h i)
           (let ((p (n a b c d e f)))
             (p (+ g h) (+ h i))))))
     
     (define make-rtd1
       (protocol1 (rtd-constructor rtd1)))
     
     (define make-rtd2
       (let ((maker2 (rtd-constructor rtd2)))
         (protocol2
          (protocol1
           (lambda (x1 x2)
             (lambda (x3 x4)
               (maker2 x1 x2 x3 x4)))))))
     
     (define make-rtd3
       (let ((maker3 (rtd-constructor rtd3)))
         (protocol3
          (protocol2
           (protocol1
            (lambda (x1 x2)
              (lambda (x3 x4)
                (lambda (x5 x6)
                  (maker3 x1 x2 x3 x4 x5 x6)))))))))
     
     (make-rtd3 1 2 3 4 5 6 7 8 9)
     
     ; evaluates to a record whose fields contain
     ; 3 5 9 11 15 17
</pre>
   <p>The purpose of the <acronym>R6RS</acronym> record&ndash;constructor descriptors is to
automate the idiom shown in the definitions of <code>make-rtd1</code>,
<code>make-rtd2</code>, and <code>make-rtd3</code> above, and to provide an
alternative to procedural abstraction when eliminating the duplication
of code seen in <code>make-point/abs</code> and <code>make-cpoint/abs</code> below.

<!--  -->
<h5 class="subsubheading">Example 2</h5>

<p>The second example illustrates the shadowing of fields in a parent
record&ndash;type by fields in a derived record&ndash;type.  Using
<acronym>ERR5RS</acronym> records, the second example becomes:

<pre class="example">     (define :point
       (make-rtd 'point '#((mutable x) (mutable y))))
     
     (define make-point (rtd-constructor :point))
     
     (define point? (rtd-predicate :point))
     (define point-x (rtd-accessor :point 'x))
     (define point-y (rtd-accessor :point 'y))
     (define point-x-set! (rtd-mutator :point 'x))
     (define point-y-set! (rtd-mutator :point 'y))
     
     (define p1 (make-point 1 2))
     (point? p1)                     &rArr; #t
     (point-x p1)                    &rArr; 1
     (point-y p1)                    &rArr; 2
     (point-x-set! p1 5)
     (point-x p1)                    &rArr; 5
     
     (define :point2
       (make-rtd 'point2 '#((mutable x) (mutable y)) :point))
     
     (define make-point2
       (rtd-constructor :point2))
     (define point2? (rtd-predicate :point2))
     (define point2-xx (rtd-accessor :point2 'x))
     (define point2-yy (rtd-accessor :point2 'y))
     
     (define p2 (make-point2 1 2 3 4))
     (point? p2)                     &rArr; #t
     (point-x p2)                    &rArr; 1
     (point-y p2)                    &rArr; 2
     (point2-xx p2)                  &rArr; 3
     (point2-yy p2)                  &rArr; 4
     
     (define make-point/abs
       (let ((maker (rtd-constructor :point)))
         (lambda (x y)
           (maker (abs x) (abs y)))))
     
     (point-x (make-point/abs -1 -2)) &rArr; 1
     (point-y (make-point/abs -1 -2)) &rArr; 2
     
     (define :cpoint
       (make-rtd 'cpoint '#((mutable rgb)) :point))
     
     (define make-cpoint
       (let ((maker (rtd-constructor :cpoint)))
         (lambda (x y c)
           (maker x y (color-&gt;rgb c)))))
     
     (define make-cpoint/abs
       (let ((maker (rtd-constructor :cpoint)))
         (lambda (x y c)
           (maker (abs x) (abs y) (color-&gt;rgb c)))))
     
     (define cpoint-rgb
       (rtd-accessor :cpoint 'rgb))
     
     (define (color-&gt;rgb c)
       (cons 'rgb c))
     
     (cpoint-rgb (make-cpoint -1 -3 'red))   &rArr; (rgb . red)
     (point-x (make-cpoint -1 -3 'red))      &rArr; -1
     (point-x (make-cpoint/abs -1 -3 'red))  &rArr; 1
</pre>
   <!-- page -->
   </body></html>

