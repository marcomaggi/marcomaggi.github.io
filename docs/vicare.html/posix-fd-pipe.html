<html lang="en">
<head>
<title>posix fd pipe - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="posix-fd.html#posix-fd" title="posix fd">
<link rel="prev" href="posix-fd-dup.html#posix-fd-dup" title="posix fd dup">
<link rel="next" href="posix-fd-fifo.html#posix-fd-fifo" title="posix fd fifo">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d18 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package is distributed under the terms of the GNU General Public
License (GPL) and can be downloaded from:

               `http://code.google.com/p/vicare-scheme/'


or:

            `http://github.com/marcomaggi/vicare/downloads'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="posix-fd-pipe"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="posix-fd-fifo.html#posix-fd-fifo">posix fd fifo</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="posix-fd-dup.html#posix-fd-dup">posix fd dup</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="posix-fd.html#posix-fd">posix fd</a>
<hr>
</div>

<h4 class="subsection">5.12.10 File descriptor pipes</h4>

<div class="defun">
&mdash; Function: <b>pipe</b><var><a name="index-pipe-882"></a></var><br>
<blockquote><p>Interface to the C function <code>pipe()</code>, <a href="../libc/Creating-a-Pipe.html#Creating-a-Pipe">pipe</a>.  Create a pair of file descriptors connected through a pipe.  If
successful return two values being fixnums representing the file
descriptors, else raise an exception.

        <p>Simple example of process writing to itself through a pipe:

     <pre class="example">          #!r6rs
          (import (vicare)
            (prefix (vicare posix) px.))
          
          (let-values (((in ou) (px.pipe)))
            (px.write ou '#vu8(1 2 3 4) 4)
            (let ((bv (make-bytevector 4)))
              (px.read in bv 4)
              bv))
          &rArr; #vu8(1 2 3 4)
</pre>
        <p class="noindent">here is how to fork a process and setup stdin and stdout in the children
to use newly created pipe file descriptors to communicate with the
parent:

     <pre class="example">          #!r6rs
          (import (vicare)
            (prefix (vicare posix) px.))
          
          (let-values
              (((child-stdin       parent-to-child) (px.pipe))
               ((parent-from-child child-stdout)    (px.pipe)))
            (fork
          
              (lambda (pid) ;parent
                (let ((buf (make-bytevector 1)))
                  (px.read  parent-from-child buf 1)
                  (px.write parent-to-child '#vu8(2) 1)
                  buf))
          
              (lambda () ;child
                (begin ;setup stdin
                  (close-input-port (current-input-port))
                  (px.dup2 child-stdin 0)
                  (px.close child-stdin))
          
                (begin ;setup stdout
                  (close-output-port (current-output-port))
                  (px.dup2 child-stdout 1)
                  (px.close child-stdout))
          
                (let ((buf (make-bytevector 1)))
                  (px.write 1 '#vu8(1) 1)
                  (px.read  0 buf 1)
                  (exit 0)))))
          &rArr; #vu8(1)
</pre>
        <p class="noindent">here is how to fork a process and setup stdin and stdout in the children
to use newly created pipe file descriptors, through Scheme ports, to
communicate with the parent:

     <pre class="example">          #!r6rs
          (import (vicare)
            (prefix (vicare posix) px.))
          
          (let-values (((child-stdin       parent-to-child) (px.pipe))
                       ((parent-from-child child-stdout)    (px.pipe)))
            (fork
             (lambda (pid)        ;parent
               (let* ((inp (make-textual-file-descriptor-input-port
                            parent-from-child "in" (native-transcoder)))
                      (oup (make-textual-file-descriptor-output-port
                            parent-to-child "out" (native-transcoder)))
                      (buf (get-string-n inp 4)))
                 (display "hello" oup)
                 (flush-output-port oup)
                 buf))
          
             (lambda ()           ;child
          
               (begin      ;setup stdin
                 (close-input-port (current-input-port))
                 (current-input-port
                  (make-textual-file-descriptor-input-port
                   child-stdin "*stdin*" (native-transcoder))))
          
               (begin ;setup stdout
                 (close-output-port (current-output-port))
                 (current-output-port
                  (make-textual-file-descriptor-output-port
                   child-stdout "*stdout*" (native-transcoder))))
          
               (display "ciao")
               (flush-output-port (current-output-port))
               (get-string-n (current-input-port) 5)
               (exit 0))))
          &rArr; "ciao"
</pre>
        </blockquote></div>

<!-- page -->
   </body></html>

