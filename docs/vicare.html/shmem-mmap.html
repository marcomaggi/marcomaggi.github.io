<html lang="en">
<head>
<title>shmem mmap - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="shmem.html#shmem" title="shmem">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d19 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package is distributed under the terms of the GNU General Public
License (GPL) and can be downloaded from:

               `http://code.google.com/p/vicare-scheme/'


or:

            `http://github.com/marcomaggi/vicare/downloads'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="shmem-mmap"></a>
<p>
Up:&nbsp;<a rel="up" accesskey="u" href="shmem.html#shmem">shmem</a>
<hr>
</div>

<h3 class="appendixsec">F.1 Shared memory through <code>mmap</code></h3>

<p>This appendix shows some sample code describing how to exchange binary
and Scheme data among multiple Vicare processes through shared
memory.  It is assumed that Vicare is installed with the
<acronym>POSIX</acronym> interface enabled.

   <p>The first example just exchanges a signed integer:

<pre class="example">     #!r6rs
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform constants)
       (vicare syntactic-extensions))
     
     (let* ((shmem.len (px.sysconf _SC_PAGESIZE))
            (shmem.ptr (px.mmap #f shmem.len
                          (fxior PROT_READ PROT_WRITE)
                          (fxior MAP_SHARED MAP_ANONYMOUS)
                          0 0)))
       (unwind-protect
           (px.fork
     
             ;; Parent: write a signed int, wait for the
             ;; child to exit, read the incremented int.
             (lambda (child-pid)
               (pointer-set-c-signed-int! shmem.ptr 0 234)
               (px.waitpid child-pid 0)
               (pointer-ref-c-signed-int shmem.ptr 0))
     
             ;; Child: wait for a while, then read a signed
             ;; int and write it back incremented by 1000;
             ;; finally exit.
             (lambda ()
               (px.nanosleep 1 0)
               (pointer-set-c-signed-int! shmem.ptr 0
                 (+ 1000 (pointer-ref-c-signed-int shmem.ptr 0)))
                 (exit)))
     
         (px.munmap shmem.ptr shmem.len)))
     &rArr; 1234
</pre>
   <p>The second example exchanges a bytevector of known length:

<pre class="example">     #!r6rs
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform constants)
       (vicare syntactic-extensions))
     
     (let* ((shmem.len (px.sysconf _SC_PAGESIZE))
            (shmem.ptr (px.mmap #f shmem.len
                                (fxior PROT_READ PROT_WRITE)
                                (fxior MAP_SHARED MAP_ANONYMOUS)
                                0 0)))
       (unwind-protect
           (px.fork
     
             ;; Parent: write the bytevector, wait for the
             ;; child to exit, read back the bytevector.
             (lambda (child-pid)
               (memory-copy shmem.ptr 0
                 '#vu8(0 1 2 3 4 5 6 7 8 9) 0 10)
               (px.waitpid child-pid 0)
               (let ((bv (make-bytevector 10)))
                 (memory-copy bv 0 shmem.ptr 0 10)
                 bv))
     
             ;; Child: wait for a while, read the bytevector,
             ;; increment the bytes, write the bytevector,
             ;; finally exit.
             (lambda ()
               (px.nanosleep 1 0)
               (let ((bv (make-bytevector 10)))
                 (memory-copy bv 0 shmem.ptr 0 10)
                 (do ((i 0 (+ 1 i)))
                     ((= i 10)
                      (memory-copy shmem.ptr 0 bv 0 10)
                      (exit))
                   (bytevector-u8-set! bv i
                     (+ 100 (bytevector-u8-ref bv i)))))))
     
         (px.munmap shmem.ptr shmem.len)))
     &rArr; #vu8(100 101 102 103 104 105 106 107 108 109)
</pre>
   <p>The third example shares a Scheme datum of length unknown by using
<code>fasl-write</code> and <code>fasl-read</code>; the number of bytes is exchanged
in the first location of shared memory, we assume that 16 pages
of memory are enough:

<pre class="example">     #!r6rs
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform constants)
       (vicare syntactic-extensions))
     
     (define data
       '(1 2 #(3 4) ciao "hello" #vu8(99 98 97)))
     
     (let* ((shmem.len (* 16 (px.sysconf _SC_PAGESIZE)))
            (shmem.ptr (px.mmap #f shmem.len
                          (fxior PROT_READ PROT_WRITE)
                          (fxior MAP_SHARED MAP_ANONYMOUS)
                          0 0)))
       (unwind-protect
           (px.fork
     
              ;; Parent: wait for the child process, read the data
              ;; length, allocate a bytevector, read data.
              (lambda (child-pid)
                (px.waitpid child-pid 0)
                (let* ((bv.len (pointer-ref-c-signed-int shmem.ptr 0))
                       (bv     (make-bytevector bv.len)))
                  (memory-copy bv 0 shmem.ptr SIZEOF_INT bv.len)
                  (fasl-read (open-bytevector-input-port bv))))
     
              ;; Child: convert Scheme data into a fasl bytevector,
              ;; write data length, write data, finally exit.
              (lambda ()
                (let-values (((port getter)
                             (open-bytevector-output-port)))
                  (fasl-write data port)
                  (let* ((bv     (getter))
                         (bv.len (bytevector-length bv)))
                    (pointer-set-c-signed-int! shmem.ptr 0 bv.len)
                    (memory-copy shmem.ptr SIZEOF_INT bv 0 bv.len)
                    (exit))))
     
         (px.munmap shmem.ptr shmem.len))))
     &rArr; (1 2 #(3 4) ciao "hello" #vu8(99 98 97))
</pre>
   <!-- page -->
   </body></html>

