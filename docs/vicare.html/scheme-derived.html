<html lang="en">
<head>
<title>scheme derived - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="scheme.html#scheme" title="scheme">
<link rel="prev" href="scheme-expansion.html#scheme-expansion" title="scheme expansion">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.3d1 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package, including its documentation, is distributed under the terms
of the GNU General Public License (GPL) and can be downloaded
from:

        `http://sourceforge.net/projects/vicare-scheme/files/'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


and at:

           `http://sourceforge.net/projects/vicare-scheme/'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2013 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

Copyright (C) Michael Sperber, R. Kent Dybvig, Matthew Flatt and Anton Van Straaten.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors, see the ``History'' appendix for details.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="scheme-derived"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="scheme-expansion.html#scheme-expansion">scheme expansion</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="scheme.html#scheme">scheme</a>
<hr>
</div>

<h3 class="section">3.11 Sample definitions for derived forms</h3>

<p>This appendix contains sample definitions for some of the keywords
described in this report in terms of simpler forms:

<!--  -->
<h5 class="subsubheading"><code>cond</code></h5>

<p>The <code>cond</code> keyword could be defined in terms of <code>if</code>,
<code>let</code> and <code>begin</code> using <code>syntax-rules</code> as follows:

<pre class="example">     (define-syntax cond
       (syntax-rules (else =&gt;)
         ((cond (else result1 result2 ...))
          (begin result1 result2 ...))
         ((cond (test =&gt; result))
          (let ((temp test))
            (if temp (result temp))))
         ((cond (test =&gt; result) clause1 clause2 ...)
          (let ((temp test))
            (if temp
                (result temp)
                (cond clause1 clause2 ...))))
         ((cond (test)) test)
         ((cond (test) clause1 clause2 ...)
          (let ((temp test))
            (if temp
                temp
                (cond clause1 clause2 ...))))
         ((cond (test result1 result2 ...))
          (if test (begin result1 result2 ...)))
         ((cond (test result1 result2 ...)
                clause1 clause2 ...)
          (if test
              (begin result1 result2 ...)
              (cond clause1 clause2 ...)))))
</pre>
   <!--  -->
<h5 class="subsubheading"><code>case</code></h5>

<p>The <code>case</code> keyword could be defined in terms of <code>let</code>,
<code>cond</code>, and <code>memv</code> using <code>syntax-rules</code> as follows:

<pre class="example">     (define-syntax case
       (syntax-rules (else)
         ((case expr0
            ((key ...) res1 res2 ...)
            ...
            (else else-res1 else-res2 ...))
          (let ((tmp expr0))
            (cond
              ((memv tmp '(key ...)) res1 res2 ...)
              ...
              (else else-res1 else-res2 ...))))
         ((case expr0
            ((keya ...) res1a res2a ...)
            ((keyb ...) res1b res2b ...)
            ...)
          (let ((tmp expr0))
            (cond
              ((memv tmp '(keya ...)) res1a res2a ...)
              ((memv tmp '(keyb ...)) res1b res2b ...)
              ...)))))
</pre>
   <!--  -->
<h5 class="subsubheading"><code>let*</code></h5>

<p>The <code>let*</code> keyword could be defined in terms of <code>let</code> using
<code>syntax-rules</code> as follows:

<pre class="example">     (define-syntax let*
       (syntax-rules ()
         ((let* () body1 body2 ...)
          (let () body1 body2 ...))
         ((let* ((name1 expr1) (name2 expr2) ...)
            body1 body2 ...)
          (let ((name1 expr1))
            (let* ((name2 expr2) ...)
              body1 body2 ...)))))
</pre>
   <!--  -->
<h5 class="subsubheading"><code>letrec</code></h5>

<p>The <code>letrec</code> keyword could be defined approximately in terms of
<code>let</code> and <code>set!</code> using <code>syntax-rules</code>, using a helper to
generate the temporary variables needed to hold the values before the
assignments are made, as follows:

<pre class="example">     (define-syntax letrec
       (syntax-rules ()
         ((letrec () body1 body2 ...)
          (let () body1 body2 ...))
         ((letrec ((var init) ...) body1 body2 ...)
          (letrec-helper
            (var ...)
            ()
            ((var init) ...)
            body1 body2 ...))))
     
     (define-syntax letrec-helper
       (syntax-rules ()
         ((letrec-helper
            ()
            (temp ...)
            ((var init) ...)
            body1 body2 ...)
          (let ((var &lt;undefined&gt;) ...)
            (let ((temp init) ...)
              (set! var temp)
              ...)
            (let () body1 body2 ...)))
         ((letrec-helper
            (x y ...)
            (temp ...)
            ((var init) ...)
            body1 body2 ...)
          (letrec-helper
            (y ...)
            (newtemp temp ...)
            ((var init) ...)
            body1 body2 ...))))
</pre>
   <p>The syntax <code>&lt;undefined&gt;</code> represents an expression that returns
something that, when stored in a location, causes an exception with
condition type <code>&amp;assertion</code> to be raised if an attempt to read from
or write to the location occurs before the assignments generated by the
<code>letrec</code> transformation take place.  (No such expression is defined
in Scheme.)

<!--  -->
<h5 class="subsubheading"><code>letrec*</code></h5>

<p>The <code>letrec*</code> keyword could be defined approximately in terms of
<code>let</code> and <code>set!</code> using <code>syntax-rules</code> as follows:

<pre class="example">     (define-syntax letrec*
       (syntax-rules ()
         ((letrec* ((var1 init1) ...) body1 body2 ...)
          (let ((var1 &lt;undefined&gt;) ...)
            (set! var1 init1)
            ...
            (let () body1 body2 ...)))))
</pre>
   <p>The syntax <code>&lt;undefined&gt;</code> is as in the definition of <code>letrec</code>
above.

<!--  -->
<h5 class="subsubheading"><code>let-values</code></h5>

<p>The following definition of <code>let-values</code> using <code>syntax-rules</code>
employs a pair of helpers to create temporary names for the formals.

<pre class="example">     (define-syntax let-values
       (syntax-rules ()
         ((let-values (binding ...) body1 body2 ...)
          (let-values-helper1
            ()
            (binding ...)
            body1 body2 ...))))
     
     (define-syntax let-values-helper1
       ;; map over the bindings
       (syntax-rules ()
         ((let-values
            ((id temp) ...)
            ()
            body1 body2 ...)
          (let ((id temp) ...) body1 body2 ...))
         ((let-values
            assocs
            ((formals1 expr1) (formals2 expr2) ...)
            body1 body2 ...)
          (let-values-helper2
            formals1
            ()
            expr1
            assocs
            ((formals2 expr2) ...)
            body1 body2 ...))))
     
     (define-syntax let-values-helper2
       ;; create temporaries for the formals
       (syntax-rules ()
         ((let-values-helper2
            ()
            temp-formals
            expr1
            assocs
            bindings
            body1 body2 ...)
          (call-with-values
            (lambda () expr1)
            (lambda temp-formals
              (let-values-helper1
                assocs
                bindings
                body1 body2 ...))))
         ((let-values-helper2
            (first . rest)
            (temp ...)
            expr1
            (assoc ...)
            bindings
            body1 body2 ...)
          (let-values-helper2
            rest
            (temp ... newtemp)
            expr1
            (assoc ... (first newtemp))
            bindings
            body1 body2 ...))
         ((let-values-helper2
            rest-formal
            (temp ...)
            expr1
            (assoc ...)
            bindings
            body1 body2 ...)
          (call-with-values
            (lambda () expr1)
            (lambda (temp ... . newtemp)
              (let-values-helper1
                (assoc ... (rest-formal newtemp))
                bindings
                body1 body2 ...))))))
</pre>
   <!--  -->
<h5 class="subsubheading"><code>let*-values</code></h5>

<p>The following macro defines <code>let*-values</code> in terms of <code>let</code>
and <code>let-values</code> using <code>syntax-rules</code>:

<pre class="example">     (define-syntax let*-values
       (syntax-rules ()
         ((let*-values () body1 body2 ...)
          (let () body1 body2 ...))
         ((let*-values (binding1 binding2 ...)
            body1 body2 ...)
          (let-values (binding1)
            (let*-values (binding2 ...)
              body1 body2 ...)))))
</pre>
   <!--  -->
<h5 class="subsubheading"><code>let</code></h5>

<p>The <code>let</code> keyword could be defined in terms of <code>lambda</code> and
<code>letrec</code> using <code>syntax-rules</code> as follows:

<pre class="example">     (define-syntax <code>let</code>
       (syntax-rules ()
         ((let ((name val) ...) body1 body2 ...)
          ((lambda (name ...) body1 body2 ...)
           val ...))
         ((let tag ((name val) ...) body1 body2 ...)
          ((letrec ((tag (lambda (name ...)
                           body1 body2 ...)))
             tag)
           val ...))))
</pre>
   <!-- end of file -->
   </body></html>

