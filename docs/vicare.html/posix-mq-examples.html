<html lang="en">
<head>
<title>posix mq examples - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="posix-mq.html#posix-mq" title="posix mq">
<link rel="prev" href="posix-mq-api.html#posix-mq-api" title="posix mq api">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d18 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package is distributed under the terms of the GNU General Public
License (GPL) and can be downloaded from:

               `http://code.google.com/p/vicare-scheme/'


or:

            `http://github.com/marcomaggi/vicare/downloads'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="posix-mq-examples"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="posix-mq-api.html#posix-mq-api">posix mq api</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="posix-mq.html#posix-mq">posix mq</a>
<hr>
</div>

<h4 class="subsection">5.21.3 Simple examples of message queues usage</h4>

<p>The following example shows how to create and close a message queue and
how to send and receive a message:

<pre class="example">     #!vicare
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform-constants)
       (vicare syntactic-extensions))
     
     (define name "/vicare-test")
     
     (define (parent child-pid)
       ;; Create a message queue and block until a message
       ;; is received from the child.
       ;;
       (let ((mqd (px.mq-open name
                              (bitwise-ior O_CREAT O_EXCL O_RDWR)
                              (bitwise-ior S_IRUSR S_IWUSR)
                              (px.make-struct-mq-attr 0 3 16 0)))
             (buf (make-bytevector 16)))
         (unwind-protect
             (let-values (((len priority)
                           (px.mq-receive mqd buf)))
               (px.waitpid child-pid 0)
               (list (subbytevector-u8 buf 0 len)
                     priority))
           (px.mq-close mqd)
           (px.mq-unlink name))))
     
     (define (child)
       ;; Wait for the parent to create the queue, then
       ;; send a message and exit.
       ;;
       (px.nanosleep 0 900000)
       (let ((mqd (px.mq-open name O_RDWR
                              (bitwise-ior S_IRUSR S_IWUSR)
                              (px.make-struct-mq-attr 0 3 16 0))))
         (unwind-protect
             (px.mq-send mqd '#ve(ascii "ciao") 1)
           (px.mq-close mqd)))
       (exit 0))
     
     (px.fork parent child)
</pre>
   <p>The following example shows how to create and close a message queue and
how to send and receive a message with timeout:

<pre class="example">     #!vicare
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform-constants)
       (vicare syntactic-extensions))
     
     (define name "/vicare-test")
     
     (define timeout
       (let ((T (px.clock-gettime CLOCK_REALTIME
                   (px.make-struct-timespec 0 0))))
         (px.set-struct-timespec-tv_sec! T
            (+ 5 (px.struct-timespec-tv_sec T)))
         T))
     
     (define (parent child-pid)
       ;; Create a message queue and block with timeout
       ;; until a message is received from the child.
       ;;
       (let ((mqd (px.mq-open name
                              (bitwise-ior O_CREAT O_EXCL O_RDWR)
                              (bitwise-ior S_IRUSR S_IWUSR)
                              (px.make-struct-mq-attr 0 3 16 0)))
             (buf (make-bytevector 16)))
         (unwind-protect
             (let-values (((len priority)
                           (px.mq-timedreceive mqd buf timeout)))
               (px.waitpid child-pid 0)
               (list (subbytevector-u8 buf 0 len)
                     priority))
           (px.mq-close mqd)
           (px.mq-unlink name))))
     
     (define (child)
       ;; Wait for the parent to create the queue, then
       ;; send a message with timeout and exit.
       ;;
       (px.nanosleep 0 900000)
       (let ((mqd (px.mq-open name
                              O_RDWR
                              (bitwise-ior S_IRUSR S_IWUSR)
                              (px.make-struct-mq-attr 0 3 16 0))))
         (unwind-protect
             (px.mq-timedsend mqd '#ve(ascii "ciao") 1 timeout)
           (px.mq-close mqd)))
       (exit 0))
     
     (px.fork parent child)
</pre>
   <!-- page -->
   </body></html>

