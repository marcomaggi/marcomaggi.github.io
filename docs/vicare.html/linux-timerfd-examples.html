<html lang="en">
<head>
<title>linux timerfd examples - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="linux-timerfd.html#linux-timerfd" title="linux timerfd">
<link rel="prev" href="linux-timerfd-api.html#linux-timerfd-api" title="linux timerfd api">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.3d1 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package, including its documentation, is distributed under the terms
of the GNU General Public License (GPL) and can be downloaded
from:

        `http://sourceforge.net/projects/vicare-scheme/files/'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


and at:

           `http://sourceforge.net/projects/vicare-scheme/'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2013 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

Copyright (C) Michael Sperber, R. Kent Dybvig, Matthew Flatt and Anton Van Straaten.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors, see the ``History'' appendix for details.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="linux-timerfd-examples"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="linux-timerfd-api.html#linux-timerfd-api">linux timerfd api</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="linux-timerfd.html#linux-timerfd">linux timerfd</a>
<hr>
</div>

<h4 class="subsection">11.6.3 Usage examples of the <code>timerfd</code> <acronym>API</acronym></h4>

<p>The following program watches a timer as time goes by:

<pre class="example">     #!r6rs
     (import (vicare)
       (vicare platform constants)
       (only (vicare language-extensions syntaxes)
             unwind-protect)
       (prefix (vicare posix) px.)
       (prefix (vicare linux) lx.))
     
     (set-port-buffer-mode! (current-output-port)
                            (buffer-mode none))
     
     (define (%print-remaining-time fd)
       (pretty-print (lx.struct-itimerspec-it_value
                      (lx.timerfd-gettime fd))))
     
     (define (%sleep-one-second)
       (px.nanosleep 1 0))
     
     (let ((fd (lx.timerfd-create CLOCK_REALTIME)))
       (unwind-protect
           (let ( ;; one event every 3 seconds
                 (period (lx.make-struct-timespec 3 0))
                 ;; the first event after 1 nanosecond
                 (offset (lx.make-struct-timespec 0 1)))
             (lx.timerfd-settime fd 0
                (lx.make-struct-itimerspec period offset))
             (do ((i 0 (fx+ 1 i)))
                 ((fx= i 6))
               (%print-remaining-time fd)
               (%sleep-one-second))
             #f)
         (px.close fd)))
</pre>
   <p class="noindent">the output is:

<pre class="example">     #["struct-timespec" tv_sec=2 tv_nsec=999911022]
     #["struct-timespec" tv_sec=1 tv_nsec=999307638]
     #["struct-timespec" tv_sec=0 tv_nsec=998303788]
     #["struct-timespec" tv_sec=2 tv_nsec=997338092]
     #["struct-timespec" tv_sec=1 tv_nsec=996393490]
     #["struct-timespec" tv_sec=0 tv_nsec=995418649]
</pre>
   <p>The following example prints the number of timer expirations:

<pre class="example">     #!r6rs
     (import (vicare)
       (vicare platform constants)
       (only (vicare language-extensions syntaxes)
             unwind-protect)
       (prefix (vicare posix) px.)
       (prefix (vicare linux) lx.))
     
     (set-port-buffer-mode! (current-output-port)
                            (buffer-mode none))
     (define fd
       (lx.timerfd-create CLOCK_REALTIME TFD_NONBLOCK))
     (unwind-protect
         (begin
           ;; 0.3 seconds = 300 ms = 300,000 us = 300,000,000 ns
           (define nsecs  300000000)
     ;;;                 9876543210
           (lx.timerfd-settime fd 0
             (lx.make-struct-itimerspec
              ;; one event every 0.3 seconds
              (lx.make-struct-timespec 0 nsecs)
              ;; the first event after 1 nanosecond
              (lx.make-struct-timespec 0 1)))
           (printf "right after starting timer: ~a\n"
             (lx.timerfd-read fd))
           (px.nanosleep 1 0)
           (printf "after 1 second: ~a\n"
             (lx.timerfd-read fd))
           (px.nanosleep 0 nsecs)
           (printf "after 0.3 seconds: ~a\n"
             (lx.timerfd-read fd)))
       (px.close fd))
</pre>
   <p class="noindent">the output is:

<pre class="example">     right after starting timer: 1
     after 1 second: 3
     after 0.3 seconds: 1
</pre>
   <p class="noindent">we notice that right after starting the timer: the number of expirations
is 1 because the timer starts 1 nanosecond after the call
to <code>timerfd-settime</code>, which is almost immediately.

<!-- page -->
   </body></html>

