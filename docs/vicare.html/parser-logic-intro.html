<html lang="en">
<head>
<title>parser logic intro - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="parser-logic.html#parser-logic" title="parser logic">
<link rel="next" href="parser-logic-operators.html#parser-logic-operators" title="parser logic operators">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.3d1 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package, including its documentation, is distributed under the terms
of the GNU General Public License (GPL) and can be downloaded
from:

        `http://sourceforge.net/projects/vicare-scheme/files/'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


and at:

           `http://sourceforge.net/projects/vicare-scheme/'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2013 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

Copyright (C) Michael Sperber, R. Kent Dybvig, Matthew Flatt and Anton Van Straaten.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors, see the ``History'' appendix for details.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="parser-logic-intro"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="parser-logic-operators.html#parser-logic-operators">parser logic operators</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="parser-logic.html#parser-logic">parser logic</a>
<hr>
</div>

<h4 class="subsection">16.8.1 Introductory examples</h4>

<h5 class="subsubheading">Parsing a string of selected characters</h5>

<p>As first usage example, let's see a simple parser using a full Scheme
string as argument and accepting lexemes being the empty string or
strings of characters &lsquo;<samp><span class="samp">#\a</span></samp>&rsquo; and &lsquo;<samp><span class="samp">\#b</span></samp>&rsquo;; the result of a call to
the parser is the list of characters or <code>#f</code> if the input is
invalid:

<pre class="example">     #!r6rs
     (import (vicare)
       (vicare parser-logic))
     
     (module (parse-abba)
     
       (define (parse-abba input-string)
         (assert (string? input-string))
         (%parse-string input-string
                        (string-length input-string)
                        0    ;start index
                        '()  ;start value for ACCUMULATOR
                        ))
     
       (define-parser-logic define-string-&gt;abba-parser ch next fail
         (%parse-string (accumulator)
            ((:end-of-input)
             (reverse accumulator))
            ((#\a #\b)
             (next %parse-string (cons ch accumulator)))))
     
       (define-string-&gt;abba-parser string-&gt;token-or-false
         (%parse-string))
     
       #| end of module |# )
     
     (parse-abba "")         &rArr; ()
     (parse-abba "a")        &rArr; (#\a)
     (parse-abba "b")        &rArr; (#\b)
     (parse-abba "1")        &rArr; #f)
     (parse-abba "ciao")     &rArr; #f)
     (parse-abba "abb")      &rArr; (#\a #\b #\b)
</pre>
   <p class="noindent">notice the use of <code>next</code> to recursively tail&ndash;call
<code>parse-string</code>.

   <p>The macro <code>string-&gt;token-or-false</code> is exported by <code>(vicare
parser-logic)</code>; it implements the device logic for a full input Scheme
string representing a lexeme; it is to be used in a parser returning
<code>#f</code> when the input is invalid.

   <p>The macros in the module combine their output and expand to the
definition of a function <code>%parse-string</code> equivalent to the
following:

<pre class="example">     (define (%parse-string input.string input.length input.index
                            accumulator)
       (if (fx=? input.index input.length)
           (reverse accumulator)
         (let ((ch (string-ref input.string input.index)))
           (cond ((or (char=? #\a ch)
                      (char=? #\b ch))
                  (%parse-string input.string input.length
                                 (fx+ 1 input.index)
                                 (cons ch accumulator)))
                 (else #f)))))
</pre>
   <!--  -->
<h5 class="subsubheading">Parsing a string of intermixed selected characters</h5>

<p>Let's see a parser using a full Scheme string as argument and accepting
lexemes being the empty string or strings of characters &lsquo;<samp><span class="samp">#\a</span></samp>&rsquo; and
&lsquo;<samp><span class="samp">\#b</span></samp>&rsquo;:

     <ul>
<li>&lsquo;<samp><span class="samp">#\a</span></samp>&rsquo; can be followed only by &lsquo;<samp><span class="samp">#\b</span></samp>&rsquo; or end&ndash;of&ndash;input.

     <li>&lsquo;<samp><span class="samp">#\b</span></samp>&rsquo; can be followed only by &lsquo;<samp><span class="samp">#\a</span></samp>&rsquo; or end&ndash;of&ndash;input. 
</ul>

<p class="noindent">the result of a call to the parser is the list of characters or <code>#f</code>
if the input is invalid:

<pre class="example">     #!r6rs
     (import (vicare)
       (vicare parser-logic))
     
     (module (parse-abab)
     
       (define (parse-abab input-string)
         (assert (string? input-string))
         (%parse-string input-string
                        (string-length input-string)
                        0    ;start index
                        '()  ;start value for ACCUMULATOR
                        ))
     
       ;;Parser logic to convert a string of intermixed
       ;;#\a and #\b into a list of characters.
       (define-parser-logic define-string-&gt;abab-parser ch next fail
         (%parse-string (accumulator)
            ((:end-of-input)
             (reverse accumulator))
            ((#\a #\b)
             (if (or (null? accumulator)
                     (case ch
                       ((#\a) (char=? #\b (car accumulator)))
                       ((#\b) (char=? #\a (car accumulator)))))
                 (next %parse-string (cons ch accumulator))
               (fail)))))
     
       ;;Actual parser drawing characters from an input string.
       (define-string-&gt;abab-parser string-&gt;token-or-false
         (%parse-string))
     
       #| end of module |# )
     
     (parse-abab "")         &rArr; ()
     (parse-abab "a")        &rArr; (#\a)
     (parse-abab "b")        &rArr; (#\b)
     (parse-abab "1")        &rArr; #f
     (parse-abab "ciao")     &rArr; #f
     (parse-abab "abb")      &rArr; #f
     (parse-abab "baa")      &rArr; #f
     (parse-abab "abab")     &rArr; (#\a #\b #\a #\b)
     (parse-abab "baba")     &rArr; (#\b #\a #\b #\a)
</pre>
   <p class="noindent">notice the use of <code>fail</code> to signal an input error from inside an
operator clause.

   <p>The macros in the module combine their output and expand to the
definition of a function <code>%parse-string</code> equivalent to the
following:

<pre class="example">     (define (%parse-string input.string input.length input.index
                            accumulator)
       (if (fx=? input.index input.length)
           (reverse accumulator)
         (let ((ch (string-ref input.string input.index)))
           (cond ((or (char=? #\a ch)
                      (char=? #\b ch))
                  (if (or (null? accumulator)
                          (case ch
                            ((#\a) (char=? #\b (car accumulator)))
                            ((#\b) (char=? #\a (car accumulator)))))
                      (%parse-string input.string input.length
                                     (fx+ 1 input.index)
                                     (cons ch accumulator))
                    #f))
                 (else #f)))))
</pre>
   <!--  -->
<h5 class="subsubheading">Parsing exact integers in base 10</h5>

<p>Let's see a parser using a full Scheme string as argument and accepting
lexemes representing exact integers in base 10; the result of a
call to the parser is the exact integer object or <code>#f</code> if the input
is invalid:

<pre class="example">     #!r6rs
     (import (vicare)
       (vicare parser-logic))
     
     (define (parse-integer input-string)
     
       (define (%digit ch)
         ;;Given a character argument: return the corresponding
         ;;fixnum if the character is between #\0 and #\9, else
         ;;return false.
         ;;
         (let ((N (fx- (char-&gt;integer ch) (char-&gt;integer #\0))))
           (and (fx&gt;= N 0)
                (fx&lt;  N 10)
                N)))
     
       ;;Parser logic to convert a string into an exact integer
       ;;in base 10.
       (define-parser-logic define-string-&gt;integer-parser ch next fail
         (%parse-integer ()
            ((%digit) =&gt; D
             (next %parse-digit+ D)))
         (%parse-digit+ (accumulator)
            ((:end-of-input)
             accumulator)
            ((%digit) =&gt; D
             (next %parse-digit+ (+ D (* 10 accumulator))))))
     
       ;;Actual parser drawing characters from an input string.
       (define-string-&gt;integer-parser string-&gt;token-or-false
         (%parse-integer))
     
       (assert (string? input-string))
       (%parse-integer input-string (string-length input-string) 0))
     
     (parse-integer "")              &rArr; #f
     (parse-integer "1")             &rArr; 1
     (parse-integer "123")           &rArr; 123
     (parse-integer "ciao")          &rArr; #f
     (parse-integer "123ciao")       &rArr; #f
</pre>
   <p>The macros in the body of <code>parse-integer</code> combine their output and
expand to the definition of two functions <code>%parse-integer</code> and
<code>%parse-digit+</code> equivalent to the following:

<pre class="example">     (define (%parse-integer input.string input.length input.index)
       (if (fx=? input.index input.length)
           #f
         (let ((ch (string-ref input.string input.index)))
           (cond ((%digit ch)
                  =&gt; (lambda (D)
                       (%parse-digit+ input.string input.length
                                      (fx+ 1 input.index) D)))
                 (else #f)))))
     
     (define (%parse-digit+ input.string input.length input.index
                            accumulator)
       (if (fx=? input.index input.length)
           accumulator
         (let ((ch (string-ref input.string input.index)))
           (cond ((%digit ch)
                  =&gt; (lambda (D)
                       (%parse-digit+ input.string input.length
                                      (fx+ 1 input.index)
                                      (+ D (* 10 accumulator)))))
                 (else #f)))))
</pre>
   <!-- page -->
   </body></html>

