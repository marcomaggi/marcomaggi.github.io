<html lang="en">
<head>
<title>iklib guardians examples - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="iklib-guardians.html#iklib-guardians" title="iklib guardians">
<link rel="next" href="iklib-guardians-api.html#iklib-guardians-api" title="iklib guardians api">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d15 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package is distributed under the terms of the GNU General Public
License (GPL) and can be downloaded from:

               `http://code.google.com/p/vicare-scheme/'


or:

            `http://github.com/marcomaggi/vicare/downloads'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="iklib-guardians-examples"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="iklib-guardians-api.html#iklib-guardians-api">iklib guardians api</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="iklib-guardians.html#iklib-guardians">iklib guardians</a>
<hr>
</div>

<h4 class="subsection">3.26.1 Usage examples for guardians</h4>

<p>Let's say that we use the Vicare <acronym>FFI</acronym> to handle some memory
block (<a href="ffi.html#ffi">ffi</a> for details on the <acronym>FFI</acronym>); memory blocks allocated
with <code>malloc</code> are not released by the Vicare garbage
collector: we have to explicitly apply <code>free</code> to the pointer value
referencing them.

   <p>If we use the blocks synchronously with the evaluation of forms, we do
(assuming we do not use continuations, so this use of
<code>dynamic-wind</code> is fine):

<pre class="example">     (import (ikarus)
       (vicare ffi))
     
     (define (do-something-with . args)
       (display args)
       (newline))
     
     (let ((p        #f)
           (size     4096))
       (dynamic-wind
           (lambda ()
             (set! p (malloc size))
             (unless p (error #f "memory allocation")))
           (lambda ()
             (do-something-with p))
           (lambda ()
             (free p))))
</pre>
   <p class="noindent">and we can define a syntax for it:

<pre class="example">     (import (ikarus)
       (vicare ffi))
     
     (define (do-something-with . args)
       (display args)
       (newline))
     
     (define-syntax with-block
       (syntax-rules ()
         ((_ ?pointer ?size ?body ...)
          (let ((?pointer    #f))
            (dynamic-wind
                (lambda ()
                  (set! ?pointer (malloc ?size))
                  (unless ?pointer (error #f "memory allocation")))
                (lambda ()
                  ?body ...)
                (lambda ()
                  (free ?pointer)))))))
     
     (with-block p 2048
       (do-something-with p))
     
     (with-block p 4096
       (do-something-with p))
     
     (with-block p 8192
       (do-something-with p))
</pre>
   <p>If we need the block in an event driven program: we will probably need
to use it asynchronously with the evaluation of forms.  For example, we
store the pointer value that references a block in a thunk (a closure
that takes no arguments):

<pre class="example">     (import (ikarus)
       (vicare ffi))
     
     (define *event-source* '())
     
     (define (enqueue-event event)
       (set! *event-source*
             (reverse (cons event (reverse *event-source*)))))
     
     (define (pop-event)
       (if (null? *event-source*)
           #f
         (let ((event (car *event-source*)))
           (set! *event-source* (cdr *event-source*))
           event)))
     
     ;; Usage:
     
     (define (do-something-with . args)
       (display args)
       (newline))
     
     (let ((p (malloc 4096)))
       (unless p (error #f "memory allocation"))
       (enqueue-event (lambda ()
                        (do-something-with p)))
       (enqueue-event (lambda ()
                        (do-something-with 123)))
       (enqueue-event (lambda ()
                        (do-something-with p))))
     
     (do ((event (pop-event) (pop-event)))
         ((not event))
       (event))
</pre>
   <p class="noindent">once the thunks have been evaluated, the pointer value is garbage
collected, but the allocated memory block becomes leaked memory.  We
need a way to be notified of the pointer value garbage collection, so
that we can apply <code>free</code> to it; this is the job for guardians.

   <p>The following script shows the usage of a guardian to free a memory
block:

<pre class="example">     (import (ikarus)
       (vicare ffi))
     
     (define g (make-guardian))
     
     (let ((a (malloc (expt 2 20))))
       (unless p (error #f "memory allocation"))
       (g a)
       (printf "value ~s~%" a))
     
     (printf "value from guardian ~s~%" (g))
     
     ;; This triggers a garbage collection.
     (collect)
     
     (let ((p (g)))
       (printf "value from guardian ~s~%" p)
       (free p))
</pre>
   <p>In an event driven program what we have to do is to register the pointer
into the guardian, and then periodically enqueue as event a call to the
guardian:

<pre class="example">     (import (ikarus)
       (vicare ffi))
     
     ;; Event source handling.
     
     (define *event-source* '())
     
     (define (enqueue-event event)
       (set! *event-source*
             (reverse (cons event (reverse *event-source*)))))
     
     (define (pop-event)
       (if (null? *event-source*)
           #f
         (let ((event (car *event-source*)))
           (set! *event-source* (cdr *event-source*))
           event)))
     
     ;; Block guardian.
     
     (define block-guardian (make-guardian))
     
     (define (run-block-guardian)
       (do ((p (block-guardian) (block-guardian)))
           ((not p))
         (printf "collecting ~s~%" p)
         (free p)))
     
     ;; Application follows.
     
     (define (do-something-with . args)
       (display args)
       (newline))
     
     (let ((p (malloc (expt 2 20))))
       (unless p (error #f "memory allocation"))
       (block-guardian p)
       (enqueue-event (lambda () (do-something-with p)))
       (enqueue-event (lambda () (do-something-with 123)))
       (enqueue-event (lambda () (do-something-with p)))
       (enqueue-event (lambda () (do-something-with 456))))
     
     (do ((event (pop-event) (pop-event))
          (i 1 (+ i 1)))
         ((= i 20))
       (when event
         (event))
       (when (= 0 (modulo i 10))
         (collect)
         (enqueue-event run-block-guardian)))
</pre>
   <!-- page -->
   </body></html>

