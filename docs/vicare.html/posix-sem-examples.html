<html lang="en">
<head>
<title>posix sem examples - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="posix-sem.html#posix-sem" title="posix sem">
<link rel="prev" href="posix-sem-api.html#posix-sem-api" title="posix sem api">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d12 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language, producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The package is distributed under the terms of the GNU General Public
License (GPL) and can be downloaded from:

               `http://code.google.com/p/vicare-scheme/'


or:

            `http://github.com/marcomaggi/vicare/downloads'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


and, as a backup, at:

                     `http://gitorious.org/vicare'


this project has a mailing list:

             `http://groups.google.com/group/vicare-users'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="posix-sem-examples"></a>
<p>
Previous:&nbsp;<a rel="previous" accesskey="p" href="posix-sem-api.html#posix-sem-api">posix sem api</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="posix-sem.html#posix-sem">posix sem</a>
<hr>
</div>

<h4 class="subsection">5.21.2 Usage examples of semaphores</h4>

<p>The following example shows how two processes can synchronise themselves
to exchange a signed integer using a <acronym>POSIX</acronym> shared memory object and
an unnamed <acronym>POSIX</acronym> semaphore allocated in such an object; the parent
process sets up the shared memory and initialises the semaphore, then it
waits for the semaphore and reads the integer; the child process writes
an integer in the shared memory, then it posts the semaphore.

   <p>Notice that the informations the two processes share are:

     <ol type=1 start=1>
<li>The pathname of the shared memory object.

     <li>The dimension of the allocated shared memory.

     <li>The fact that the semaphore is allocated at the beginning of the shared
memory.

     <li>The fact that the signed integer exchange area is right after the
semaphore structure in the shared memory.
        </ol>

<pre class="example">     #!r6rs
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform-constants)
       (only (vicare syntactic-extensions)
             unwind-protect
             callet))
     
     (define shm.pathname
       "/vicare.test")
     (define shm.dim
       (px.sysconf _SC_PAGESIZE))
     
     (define (parent child-pid)
       (let ((shm.fd (callet px.shm-open shm.pathname
                       (oflags   (fxior O_CREAT O_EXCL O_RDWR))
                       (mode     (fxior S_IRUSR S_IWUSR)))))
         (px.ftruncate shm.fd shm.dim)
         (unwind-protect
             (let ((shm.base (callet px.mmap
                               (address #f)
                               (size    shm.dim)
                               (prot    (fxior PROT_READ
                                               PROT_WRITE))
                               (flags   MAP_SHARED)
                               (fd      shm.fd)
                               (offset  0))))
               (unwind-protect
                   (let* ((sem_t     shm.base)
                          (shm.start (pointer-add shm.base
                                       (px.sizeof-sem_t)))
                          (sem_t     (callet px.sem-init sem_t
                                       (pshared? #t)
                                       (value    0))))
                     (define timeout
                       (let ((T (px.clock-gettime CLOCK_REALTIME
                                  (px.make-struct-timespec 0 0))))
                         (px.set-struct-timespec-tv_sec! T
                           (+ 2 (px.struct-timespec-tv_sec T)))
                         T))
                     (unwind-protect
                         (begin
                           (px.sem-timedwait sem_t timeout)
                           (pointer-ref-c-signed-int shm.start 0))
                       (px.sem-destroy sem_t)))
                 (px.munmap shm.base shm.dim)))
           (px.close shm.fd)
           (px.shm-unlink shm.pathname))))
     
     (define (child)
       ;; Give the parent some time to open the
       ;; shared memory object.
       (px.nanosleep 1 0)
       (let ((shm.fd (callet px.shm-open shm.pathname
                       (oflags   (fxior O_CREAT O_RDWR))
                       (mode     (fxior S_IRUSR S_IWUSR)))))
         (unwind-protect
             (let ((shm.base (callet px.mmap
                               (address #f)
                               (size    shm.dim)
                               (prot    (fxior PROT_READ
                                               PROT_WRITE))
                               (flags   MAP_SHARED)
                               (fd      shm.fd)
                               (offset  0))))
               (unwind-protect
                   (let* ((sem_t     shm.base)
                          (shm.start (pointer-add shm.base
                                       (px.sizeof-sem_t))))
                     (pointer-set-c-signed-int! shm.start 0 123)
                     (px.sem-post sem_t))
                 (px.munmap shm.base shm.dim)))
           (px.close shm.fd)))
       (exit 0))
     
     (px.fork parent child)  &rArr; 123
</pre>
   <p>The following example shows how two processes can synchronise themselves
to exchange a signed integer using a <acronym>POSIX</acronym> shared memory object and a
named <acronym>POSIX</acronym> semaphore; the parent process sets up the shared memory
and the semaphore, then it waits for the semaphore and reads the
integer; the child process writes an integer in the shared memory, then
it posts the semaphore.

   <p>Notice that the informations the two processes share are:

     <ol type=1 start=1>
<li>The pathname of the shared memory object.

     <li>The pathname of the semaphore object.

     <li>The dimension of the allocated shared memory.

     <li>The fact that the signed integer exchange area is at the beginning of
the shared memory.
        </ol>

<pre class="example">     #!r6rs
     (import (vicare)
       (prefix (vicare posix) px.)
       (vicare platform-constants)
       (only (vicare syntactic-extensions)
             unwind-protect
             callet))
     
     (define sem.pathname
       "/vicare-posix-sem.test")
     (define shm.pathname
       "/vicare-posix-shm.test")
     (define shm.dim
       (px.sysconf _SC_PAGESIZE))
     
     (define (parent child-pid)
       (let ((sem_t  (callet px.sem-open sem.pathname
                       (oflags (fxior O_CREAT O_EXCL O_RDWR))
                       (mode   (fxior S_IRUSR S_IWUSR))))
             (shm.fd (callet px.shm-open shm.pathname
                       (oflags   (fxior O_CREAT O_EXCL O_RDWR))
                       (mode     (fxior S_IRUSR S_IWUSR)))))
         (px.ftruncate shm.fd shm.dim)
         (unwind-protect
             (let ((shm.base (callet px.mmap
                               (address #f)
                               (size    shm.dim)
                               (prot    (fxior PROT_READ
                                               PROT_WRITE))
                               (flags   MAP_SHARED)
                               (fd      shm.fd)
                               (offset  0))))
               (unwind-protect
                   (begin
                     (px.sem-wait sem_t)
                     (pointer-ref-c-signed-int shm.base 0))
                 (px.munmap shm.base shm.dim)))
           (px.close shm.fd)
           (px.shm-unlink shm.pathname)
           (px.sem-close sem_t)
           (px.sem-unlink sem.pathname))))
     
     (define (child)
       ;; Give the parent some time to open the
       ;; shared memory object.
       (px.nanosleep 1 0)
       (let ((sem_t  (callet px.sem-open sem.pathname
                       (oflags (fxior O_CREAT O_RDWR))
                       (mode   (fxior S_IRUSR S_IWUSR))))
             (shm.fd (callet px.shm-open shm.pathname
                       (oflags (fxior O_CREAT O_RDWR))
                       (mode   (fxior S_IRUSR S_IWUSR)))))
         (unwind-protect
             (let ((shm.base (callet px.mmap
                               (address #f)
                               (size    shm.dim)
                               (prot    (fxior PROT_READ
                                               PROT_WRITE))
                               (flags   MAP_SHARED)
                               (fd      shm.fd)
                               (offset  0))))
               (unwind-protect
                   (begin
                     (pointer-set-c-signed-int! shm.base 0 123)
                     (px.sem-post sem_t))
                 (px.munmap shm.base shm.dim)))
           (px.close shm.fd)
           (px.sem-close sem_t)))
       (exit 0))
     
     (px.fork parent child) &rArr; 123
</pre>
   <!-- page -->
   </body></html>

