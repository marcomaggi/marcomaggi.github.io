<html lang="en">
<head>
<title>iklib modules examples named - Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="iklib-modules-examples.html#iklib-modules-examples" title="iklib modules examples">
<link rel="prev" href="iklib-modules-examples-anonymous.html#iklib-modules-examples-anonymous" title="iklib modules examples anonymous">
<link rel="next" href="iklib-modules-examples-utils.html#iklib-modules-examples-utils" title="iklib modules examples utils">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes version 0.2d10 of Vicare Scheme, an
R6RS compliant native compiler for the Scheme language producing
single threaded programs running on Intel x86 32-bit processors, with
experimental support for 64-bit machines.  _Vicare_ is pronounced
the etruscan way.

The Vicarus Scheme package is distributed under the terms of the GNU
General Public License (GPL) and can be downloaded from:

            `http://github.com/marcomaggi/vicare/downloads'


the home page of the project is at:

              `http://marcomaggi.github.com/vicare.html'


development takes place at:

                `http://github.com/marcomaggi/vicare/'


Copyright (C) 2010-2012 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

This document is derived from the original Ikarus documentation by the
Vicare Scheme contributors.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="iklib-modules-examples-named"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="iklib-modules-examples-utils.html#iklib-modules-examples-utils">iklib modules examples utils</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="iklib-modules-examples-anonymous.html#iklib-modules-examples-anonymous">iklib modules examples anonymous</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="iklib-modules-examples.html#iklib-modules-examples">iklib modules examples</a>
<hr>
</div>

<h5 class="subsubsection">3.19.1.2 Named modules</h5>

<p>The following example defines a module named <code>blue</code> and invokes
functions from it in the top level environment:

<pre class="example">     (import (ikarus))
     
     [module blue (blue-one blue-two blue-three)
         (define (blue-one)          'blue-one)
         (define (blue-two)          'blue-two)
         (define (blue-three)        (cons 'blue-three (hidden)))
         (define (hidden)            'blue-hidden)]
     
     (import blue)
     (printf "calling blue: ~s ~s ~s~%"
             (blue-one) (blue-two) (blue-three))
</pre>
   <p class="noindent">notice that bindings from a named module are accessible only if imported
in the enclosing environment.

   <p>The following example defines two modules named <code>green</code> and
<code>red</code>, then it imports their bindings in different environments:

<pre class="example">     (import (ikarus))
     
     (let ()
     
       [module green (one two three)
         (define (one)       'green-one)
         (define (two)       'green-two)
         (define (three)     (cons 'green-three (hidden)))
         (define (hidden)    'green-hidden)]
     
       [module red (one two three)
         (define (one)       'red-one)
         (define (two)       'red-two)
         (define (three)     (cons 'red-three (hidden)))
         (define (hidden)    'red-hidden)]
     
       (import green)
     
       [let ()
         (import red)
         (printf "calling red: ~s ~s ~s~%" (one) (two) (three))]
     
       (printf "calling green: ~s ~s ~s~%" (one) (two) (three)))
</pre>
   <p>The following example shows that modules in the same enclosing
environment can import their bindings:

<pre class="example">     (import (ikarus))
     
     (let ()
     
       [module green (one two)
         (define (one)       'one)
         (define (two)       'two)]
     
       [module red (f g)
         (import green)
         (define (f)         (cons 'f (one)))
         (define (g)         (cons 'g (two)))]
     
       (import red)
       (printf "calling red: ~s ~s~%" (f) (g)))
</pre>
   <p class="noindent">while the following example shows that bindings from an anonymous module
are automatically available in modules defined in the same enclosing
environment:

<pre class="example">     (import (ikarus))
     
     (let ()
     
       [module (one two)
         (define (one)       'one)
         (define (two)       'two)]
     
       [module red (f g)
         (define (f)         (cons 'f (one)))
         (define (g)         (cons 'g (two)))]
     
       (import red)
       (printf "calling red: ~s ~s~%" (f) (g)))
</pre>
   <p class="noindent">and the following example shows that the order of module definitions
does not matter in determining visibility of bindings from
<strong>anonymous</strong> modules:

<pre class="example">     (import (ikarus))
     
     (let ()
     
       [module red (f g)
         (define (f)         (cons 'f (one)))
         (define (g)         (cons 'g (two)))]
     
       [module (one two)
         (define (one)       'one)
         (define (two)       'two)]
     
       (import red)
       (printf "calling red: ~s ~s~%" (f) (g)))
</pre>
   <p class="noindent">the following is an <strong>error</strong> because it tries to import bindings
from a <strong>named</strong> module before its definition:

<pre class="example">     (import (ikarus))
     
     (let ()
     
       [module red (f g)
         (import green)
         (define (f)         (cons 'f (one)))
         (define (g)         (cons 'g (two)))]
     
       [module green (one two)
         (define (one)       'one)
         (define (two)       'two)]
     
       (import red)
       (printf "calling red: ~s ~s~%" (f) (g)))
</pre>
   <!-- page -->
   </body></html>

