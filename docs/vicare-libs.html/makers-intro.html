<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- This document describes the libraries distributed along with version
0.4d1 of Vicare Scheme, an R6RS compliant native
compiler for the Scheme language.

Copyright (C) 2010-2017 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

The documentation of IrRegex is Copyright (C) 2005-2012 Alex
Shinn.  All rights reserved.

The documentation of Pregexp is Copyright (C) 1999-2005 Dorai
Sitaram.  All rights reserved.

The documentation of the library (vicare formations) is derived
from the documentation of Guile.  Copyright (C) 1996-2005,
2009-2013 Free Software Foundation.

The documentation of the libraries (vicare parser-tools silex
-) are derived form the documentation of SILex.  Copyright
(C) 2001, 2009 Danny Dube'.

This program is free software: you can redistribute it and/or modify it
under the terms of the GNU General Public License version 3 as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see
http://www.gnu.org/licenses/.

Trademarks used herein are the property of their respective owners. -->
<!-- Created by GNU Texinfo 6.3, http://www.gnu.org/software/texinfo/ -->
<head>
<title>Libraries for Vicare Scheme: makers intro</title>

<meta name="description" content="Libraries for Vicare Scheme: makers intro">
<meta name="keywords" content="Libraries for Vicare Scheme: makers intro">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<link href="index.html#Top" rel="start" title="Top">
<link href="concept-index.html#concept-index" rel="index" title="concept index">
<link href="makers.html#makers" rel="up" title="makers">
<link href="makers-api.html#makers-api" rel="next" title="makers api">
<link href="makers.html#makers" rel="prev" title="makers">
<style type="text/css">
<!--
a.summary-letter {text-decoration: none}
blockquote.indentedblock {margin-right: 0em}
blockquote.smallindentedblock {margin-right: 0em; font-size: smaller}
blockquote.smallquotation {font-size: smaller}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
div.lisp {margin-left: 3.2em}
div.smalldisplay {margin-left: 3.2em}
div.smallexample {margin-left: 3.2em}
div.smalllisp {margin-left: 3.2em}
kbd {font-style: oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
pre.smalldisplay {font-family: inherit; font-size: smaller}
pre.smallexample {font-size: smaller}
pre.smallformat {font-family: inherit; font-size: smaller}
pre.smalllisp {font-size: smaller}
span.nolinebreak {white-space: nowrap}
span.roman {font-family: initial; font-weight: normal}
span.sansserif {font-family: sans-serif; font-weight: normal}
ul.no-bullet {list-style: none}
-->
</style>


</head>

<body lang="en">
<a name="makers-intro"></a>
<div class="header">
<p>
Next: <a href="makers-api.html#makers-api" accesskey="n" rel="next">makers api</a>, Up: <a href="makers.html#makers" accesskey="u" rel="up">makers</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<a name="Introduction-to-maker-macros"></a>
<h4 class="subsection">1.8.1 Introduction to maker macros</h4>


<p>It happens to define a function or macro accepting a number of arguments
greater than 3:
</p>
<div class="example">
<pre class="example">(define (the-func a b c d e f g h i)
  ...)
</pre></div>

<p>in these cases it can be difficult to remember the order of the
arguments; it can also be desirable to define default values for some or
all of the arguments, so that at the call site some arguments can be
omitted in the source code.  The syntax <code>define-maker</code> implements a
solution to this problem.
</p>
<p>The following examples show the expansion of a simple maker with neither
fixed nor variable arguments, only optional clauses arguments:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define-maker doit
  list ((alpha:     1)
        (beta:      2)
        (gamma:     3)))

(doit)                  &rarr; (list 1 2 3)

(doit (alpha:   10))    &rarr; (list 10 2 3)
(doit (beta:    20))    &rarr; (list 1 20 3)
(doit (gamma:   30))    &rarr; (list 1 2 30)

(doit (alpha:   10)
      (beta:    20))    &rarr; (list 10 20 3)

(doit (alpha:   10)
      (gamma:   30))    &rarr; (list 10 2 30)

(doit (gamma:   30)
      (beta:    20))    &rarr; (list 1 20 30)

(doit (alpha:   10)
      (beta:    20)
      (gamma:   30))    &rarr; (list 10 20 30)

(let ((b 7))
  (doit (beta:  (+ 6 (* 2 b)))
        (alpha: (+ 2 8)))
  &rarr; (list (+ 2 8)
            (+ 6 (* 2 b))
            3)
  #f)

(doit (alpha: 10 20 30))
&rarr; (list 1 (list 10 20 30) 3)
</pre></div>

<p>notice the last example: when multiple values are used in an argument&rsquo;s
clause, they are enclosed in a <code>list</code> form; the following examples
show the expansion of a maker with both fixed and variable arguments,
plus optional clauses arguments:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define-maker (doit a b)
    (list #\a #\b)
  ((alpha:  1)
   (beta:   2)
   (gamma:  3)))


(doit #\p #\q)          &rarr; (list #\a #\b #\p #\q 1 2 3)

(doit #\p #\q
      (alpha: 10))      &rarr; (list #\a #\b #\p #\q 10 2 3)

(doit #\p #\q
      (beta: 20))       &rarr; (list #\a #\b #\p #\q 1 20 3)

(doit #\p #\q
      (gamma: 30))      &rarr; (list #\a #\b #\p #\q 1 2 30)

(doit #\p #\q
      (alpha: 10)
      (beta:  20))      &rarr; (list #\a #\b #\p #\q 10 20 3)

(doit #\p #\q
      (alpha: 10)
      (gamma: 30))      &rarr; (list #\a #\b #\p #\q 10 2 30)

(doit #\p #\q
      (gamma: 30)
      (beta:  20))      &rarr; (list #\a #\b #\p #\q 1 20 30)
</pre></div>

<p>Each default value can be any expression and it is evaluated every time
the maker is used:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define g
  (let ((counter 0))
    (lambda ()
      (set! counter (+ 1 counter))
      counter)))

(define default2 (- (/ 9 3) 1))

(define-maker doit
  list ((alpha:     1)
        (beta:      default2)
        (gamma:     (g))))

(doit)                  &rArr; ( 1  2  1)
(doit (alpha: 10))      &rArr; (10  2  2)
(doit (beta:  20))      &rArr; ( 1 20  3)
(doit (gamma: 30))      &rArr; ( 1  2 30)
</pre></div>

<p>A maker invocation can expand itself into another macro use; this allows
us to detect whether an optional argument was used or not:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define-maker doit
    subdoit
  ((alpha:  1)
   (beta:   2)
   (gamma:  sentinel)))

(define-syntax subdoit
  (lambda (stx)
    (syntax-case stx ()
      ((_ ?alpha ?beta ?gamma)
       (and (identifier? #'?gamma)
            (free-identifier=? #'?gamma #'sentinel))
       #'(list ?alpha ?beta 3))
      ((_ ?alpha ?beta ?gamma)
       #'(list ?alpha ?beta ?gamma))
      )))

(doit)                  &rArr; ( 1  2  3)
(doit (alpha: 10))      &rArr; (10  2  3)
(doit (beta:  20))      &rArr; ( 1 20  3)
(doit (gamma: 30))      &rArr; ( 1  2 30)
</pre></div>

<p>and also to &ldquo;unpack&rdquo; multiple values used in the same argument clause:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define-maker doit
  subdoit ((alpha:  1)
           (beta:   2)
           (gamma:  3)))

(define-syntax subdoit
  (lambda (stx)
    (syntax-case stx (list)
      ((_ ?alpha (list ?beta0 ...) ?gamma)
       #'(list ?alpha ?beta0 ... ?gamma))
      ((_ ?alpha ?beta ?gamma)
       #'(list ?alpha ?beta ?gamma))
      )))

(doit (alpha: 10)
       (beta: #\a #\b #\c)
       (gamma: 30))
&rArr; (10 #\a #\b #\c 30)
</pre></div>

<p>We can define a maker in the body of a library and export it; we just
have to remember to export the auxiliary syntaxes, too:
</p>
<div class="example">
<pre class="example">(library (the-lib)
  (export doit alpha beta gamma)
  (import (vicare)
    (prefix (vicare language-extensions makers) mk.))
  (define-auxiliary-syntaxes alpha beta gamma)
  (define-maker doit
    list ((alpha     1)
          (beta      2)
          (gamma     3))))

(library (exec)
  (export)
  (import (rnrs)
    (prefix (the-lib) lib.))
  (lib.doit)                  &rarr; (list 1 2 3)
  (lib.doit (lib.alpha   10)) &rarr; (list 10 2 3)
  (lib.doit (lib.beta    20)) &rarr; (list 1 20 3)
  (lib.doit (lib.gamma   30)) &rarr; (list 1 2 30)
  )
</pre></div>

<p>A number of options can be specified to customise the parsing of
clauses.  We can specify if a clause is mandatory or optional:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define-maker doit
  list ((alpha:     1 (mk.mandatory))
        (beta:      2 (mk.optional))
        (gamma:     3)))

(doit (alpha: 10))              &rarr; (list 10 2 3)
(doit (beta:  20))              error&rarr; missing clause &quot;alpha:&quot;
</pre></div>

<p>the default is for all the clauses to be optional.  We can specify that
a clause must be used along one or more other clauses:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntaxes alpha: beta: gamma:)

(define-maker doit
  list ((alpha:     1 (mk.with beta: gamma:))
        (beta:      2)
        (gamma:     3)))

(doit (alpha: 10)
      (beta:  20)
      (gamma: 30))              &rarr; (list 10 20 30)

(doit (beta: 20))               &rarr; (list 1 20 3)

(doit (alpha: 10)
      (gamma: 30))              error&rarr; missing clause &quot;beta:&quot;
</pre></div>

<p>or we can specify that a clause must not be used along one or more other
clauses:
</p>
<div class="example">
<pre class="example">(import (vicare)
  (prefix (vicare language-extensions makers) mk.))

(define-auxiliary-syntax alpha: beta: gamma:)

(define-maker doit
  list ((alpha:     1 (mk.without beta: gamma:))
        (beta:      2)
        (gamma:     3)))

(doit (alpha: 10)
      (beta:  20)
      (gamma: 30))              error&rarr; invalid clauses mix

(doit (beta: 20))               &rarr; (list 1 20 3)
</pre></div>

<hr>
<div class="header">
<p>
Next: <a href="makers-api.html#makers-api" accesskey="n" rel="next">makers api</a>, Up: <a href="makers.html#makers" accesskey="u" rel="up">makers</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
