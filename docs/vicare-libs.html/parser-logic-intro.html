<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- This document describes the libraries distributed along with version
0.4d1 of Vicare Scheme, an R6RS compliant native
compiler for the Scheme language.

Copyright (C) 2010-2017 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

The documentation of IrRegex is Copyright (C) 2005-2012 Alex
Shinn.  All rights reserved.

The documentation of Pregexp is Copyright (C) 1999-2005 Dorai
Sitaram.  All rights reserved.

The documentation of the library (vicare formations) is derived
from the documentation of Guile.  Copyright (C) 1996-2005,
2009-2013 Free Software Foundation.

The documentation of the libraries (vicare parser-tools silex
-) are derived form the documentation of SILex.  Copyright
(C) 2001, 2009 Danny Dube'.

This program is free software: you can redistribute it and/or modify it
under the terms of the GNU General Public License version 3 as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see
http://www.gnu.org/licenses/.

Trademarks used herein are the property of their respective owners. -->
<!-- Created by GNU Texinfo 6.3, http://www.gnu.org/software/texinfo/ -->
<head>
<title>Libraries for Vicare Scheme: parser logic intro</title>

<meta name="description" content="Libraries for Vicare Scheme: parser logic intro">
<meta name="keywords" content="Libraries for Vicare Scheme: parser logic intro">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<link href="index.html#Top" rel="start" title="Top">
<link href="concept-index.html#concept-index" rel="index" title="concept index">
<link href="parser-logic.html#parser-logic" rel="up" title="parser logic">
<link href="parser-logic-operators.html#parser-logic-operators" rel="next" title="parser logic operators">
<link href="parser-logic.html#parser-logic" rel="prev" title="parser logic">
<style type="text/css">
<!--
a.summary-letter {text-decoration: none}
blockquote.indentedblock {margin-right: 0em}
blockquote.smallindentedblock {margin-right: 0em; font-size: smaller}
blockquote.smallquotation {font-size: smaller}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
div.lisp {margin-left: 3.2em}
div.smalldisplay {margin-left: 3.2em}
div.smallexample {margin-left: 3.2em}
div.smalllisp {margin-left: 3.2em}
kbd {font-style: oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
pre.smalldisplay {font-family: inherit; font-size: smaller}
pre.smallexample {font-size: smaller}
pre.smallformat {font-family: inherit; font-size: smaller}
pre.smalllisp {font-size: smaller}
span.nolinebreak {white-space: nowrap}
span.roman {font-family: initial; font-weight: normal}
span.sansserif {font-family: sans-serif; font-weight: normal}
ul.no-bullet {list-style: none}
-->
</style>


</head>

<body lang="en">
<a name="parser-logic-intro"></a>
<div class="header">
<p>
Next: <a href="parser-logic-operators.html#parser-logic-operators" accesskey="n" rel="next">parser logic operators</a>, Up: <a href="parser-logic.html#parser-logic" accesskey="u" rel="up">parser logic</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<a name="Introductory-examples-1"></a>
<h3 class="section">11.1 Introductory examples</h3>


<a name="Parsing-a-string-of-selected-characters"></a>
<h4 class="subsubheading">Parsing a string of selected characters</h4>

<p>As first usage example, let&rsquo;s see a simple parser using a full Scheme
string as argument and accepting lexemes being the empty string or
strings of characters &lsquo;<samp>#\a</samp>&rsquo; and &lsquo;<samp>\#b</samp>&rsquo;; the result of a call to
the parser is the list of characters or <code>#f</code> if the input is
invalid:
</p>
<div class="example">
<pre class="example">#!r6rs
(import (vicare)
  (vicare parser-logic))

(module (parse-abba)

  (define (parse-abba input-string)
    (assert (string? input-string))
    (%parse-string input-string
                   (string-length input-string)
                   0    ;start index
                   '()  ;start value for ACCUMULATOR
                   ))

  (define-parser-logic define-string-&gt;abba-parser ch next fail
    (%parse-string (accumulator)
       ((:end-of-input)
        (reverse accumulator))
       ((#\a #\b)
        (next %parse-string (cons ch accumulator)))))

  (define-string-&gt;abba-parser string-&gt;token-or-false
    (%parse-string))

  #| end of module |# )

(parse-abba &quot;&quot;)         &rArr; ()
(parse-abba &quot;a&quot;)        &rArr; (#\a)
(parse-abba &quot;b&quot;)        &rArr; (#\b)
(parse-abba &quot;1&quot;)        &rArr; #f)
(parse-abba &quot;ciao&quot;)     &rArr; #f)
(parse-abba &quot;abb&quot;)      &rArr; (#\a #\b #\b)
</pre></div>

<p>notice the use of <code>next</code> to recursively tail&ndash;call
<code>parse-string</code>.
</p>
<p>The macro <code>string-&gt;token-or-false</code> is exported by <code>(vicare
parser-logic)</code>; it implements the device logic for a full input Scheme
string representing a lexeme; it is to be used in a parser returning
<code>#f</code> when the input is invalid.
</p>
<p>The macros in the module combine their output and expand to the
definition of a function <code>%parse-string</code> equivalent to the
following:
</p>
<div class="example">
<pre class="example">(define (%parse-string input.string input.length input.index
                       accumulator)
  (if (fx=? input.index input.length)
      (reverse accumulator)
    (let ((ch (string-ref input.string input.index)))
      (cond ((or (char=? #\a ch)
                 (char=? #\b ch))
             (%parse-string input.string input.length
                            (fx+ 1 input.index)
                            (cons ch accumulator)))
            (else #f)))))
</pre></div>


<a name="Parsing-a-string-of-intermixed-selected-characters"></a>
<h4 class="subsubheading">Parsing a string of intermixed selected characters</h4>

<p>Let&rsquo;s see a parser using a full Scheme string as argument and accepting
lexemes being the empty string or strings of characters &lsquo;<samp>#\a</samp>&rsquo; and
&lsquo;<samp>\#b</samp>&rsquo;:
</p>
<ul>
<li> &lsquo;<samp>#\a</samp>&rsquo; can be followed only by &lsquo;<samp>#\b</samp>&rsquo; or end&ndash;of&ndash;input.

</li><li> &lsquo;<samp>#\b</samp>&rsquo; can be followed only by &lsquo;<samp>#\a</samp>&rsquo; or end&ndash;of&ndash;input.
</li></ul>

<p>the result of a call to the parser is the list of characters or <code>#f</code>
if the input is invalid:
</p>
<div class="example">
<pre class="example">#!r6rs
(import (vicare)
  (vicare parser-logic))

(module (parse-abab)

  (define (parse-abab input-string)
    (assert (string? input-string))
    (%parse-string input-string
                   (string-length input-string)
                   0    ;start index
                   '()  ;start value for ACCUMULATOR
                   ))

  ;;Parser logic to convert a string of intermixed
  ;;#\a and #\b into a list of characters.
  (define-parser-logic define-string-&gt;abab-parser ch next fail
    (%parse-string (accumulator)
       ((:end-of-input)
        (reverse accumulator))
       ((#\a #\b)
        (if (or (null? accumulator)
                (case ch
                  ((#\a) (char=? #\b (car accumulator)))
                  ((#\b) (char=? #\a (car accumulator)))))
            (next %parse-string (cons ch accumulator))
          (fail)))))

  ;;Actual parser drawing characters from an input string.
  (define-string-&gt;abab-parser string-&gt;token-or-false
    (%parse-string))

  #| end of module |# )

(parse-abab &quot;&quot;)         &rArr; ()
(parse-abab &quot;a&quot;)        &rArr; (#\a)
(parse-abab &quot;b&quot;)        &rArr; (#\b)
(parse-abab &quot;1&quot;)        &rArr; #f
(parse-abab &quot;ciao&quot;)     &rArr; #f
(parse-abab &quot;abb&quot;)      &rArr; #f
(parse-abab &quot;baa&quot;)      &rArr; #f
(parse-abab &quot;abab&quot;)     &rArr; (#\a #\b #\a #\b)
(parse-abab &quot;baba&quot;)     &rArr; (#\b #\a #\b #\a)
</pre></div>

<p>notice the use of <code>fail</code> to signal an input error from inside an
operator clause.
</p>
<p>The macros in the module combine their output and expand to the
definition of a function <code>%parse-string</code> equivalent to the
following:
</p>
<div class="example">
<pre class="example">(define (%parse-string input.string input.length input.index
                       accumulator)
  (if (fx=? input.index input.length)
      (reverse accumulator)
    (let ((ch (string-ref input.string input.index)))
      (cond ((or (char=? #\a ch)
                 (char=? #\b ch))
             (if (or (null? accumulator)
                     (case ch
                       ((#\a) (char=? #\b (car accumulator)))
                       ((#\b) (char=? #\a (car accumulator)))))
                 (%parse-string input.string input.length
                                (fx+ 1 input.index)
                                (cons ch accumulator))
               #f))
            (else #f)))))
</pre></div>


<a name="Parsing-exact-integers-in-base-10"></a>
<h4 class="subsubheading">Parsing exact integers in base <em>10</em></h4>

<p>Let&rsquo;s see a parser using a full Scheme string as argument and accepting
lexemes representing exact integers in base <em>10</em>; the result of a
call to the parser is the exact integer object or <code>#f</code> if the input
is invalid:
</p>
<div class="example">
<pre class="example">#!r6rs
(import (vicare)
  (vicare parser-logic))

(define (parse-integer input-string)

  (define (%digit ch)
    ;;Given a character argument: return the corresponding
    ;;fixnum if the character is between #\0 and #\9, else
    ;;return false.
    ;;
    (let ((N (fx- (char-&gt;integer ch) (char-&gt;integer #\0))))
      (and (fx&gt;= N 0)
           (fx&lt;  N 10)
           N)))

  ;;Parser logic to convert a string into an exact integer
  ;;in base 10.
  (define-parser-logic define-string-&gt;integer-parser ch next fail
    (%parse-integer ()
       ((%digit) =&gt; D
        (next %parse-digit+ D)))
    (%parse-digit+ (accumulator)
       ((:end-of-input)
        accumulator)
       ((%digit) =&gt; D
        (next %parse-digit+ (+ D (* 10 accumulator))))))

  ;;Actual parser drawing characters from an input string.
  (define-string-&gt;integer-parser string-&gt;token-or-false
    (%parse-integer))

  (assert (string? input-string))
  (%parse-integer input-string (string-length input-string) 0))

(parse-integer &quot;&quot;)              &rArr; #f
(parse-integer &quot;1&quot;)             &rArr; 1
(parse-integer &quot;123&quot;)           &rArr; 123
(parse-integer &quot;ciao&quot;)          &rArr; #f
(parse-integer &quot;123ciao&quot;)       &rArr; #f
</pre></div>

<p>The macros in the body of <code>parse-integer</code> combine their output and
expand to the definition of two functions <code>%parse-integer</code> and
<code>%parse-digit+</code> equivalent to the following:
</p>
<div class="example">
<pre class="example">(define (%parse-integer input.string input.length input.index)
  (if (fx=? input.index input.length)
      #f
    (let ((ch (string-ref input.string input.index)))
      (cond ((%digit ch)
             =&gt; (lambda (D)
                  (%parse-digit+ input.string input.length
                                 (fx+ 1 input.index) D)))
            (else #f)))))

(define (%parse-digit+ input.string input.length input.index
                       accumulator)
  (if (fx=? input.index input.length)
      accumulator
    (let ((ch (string-ref input.string input.index)))
      (cond ((%digit ch)
             =&gt; (lambda (D)
                  (%parse-digit+ input.string input.length
                                 (fx+ 1 input.index)
                                 (+ D (* 10 accumulator)))))
            (else #f)))))
</pre></div>



<hr>
<div class="header">
<p>
Next: <a href="parser-logic-operators.html#parser-logic-operators" accesskey="n" rel="next">parser logic operators</a>, Up: <a href="parser-logic.html#parser-logic" accesskey="u" rel="up">parser logic</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
