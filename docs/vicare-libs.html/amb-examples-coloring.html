<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- This document describes the libraries distributed along with version
0.4d1 of Vicare Scheme, an R6RS compliant native
compiler for the Scheme language.

Copyright (C) 2010-2017 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

The documentation of IrRegex is Copyright (C) 2005-2012 Alex
Shinn.  All rights reserved.

The documentation of Pregexp is Copyright (C) 1999-2005 Dorai
Sitaram.  All rights reserved.

The documentation of the library (vicare formations) is derived
from the documentation of Guile.  Copyright (C) 1996-2005,
2009-2013 Free Software Foundation.

The documentation of the libraries (vicare parser-tools silex
-) are derived form the documentation of SILex.  Copyright
(C) 2001, 2009 Danny Dube'.

This program is free software: you can redistribute it and/or modify it
under the terms of the GNU General Public License version 3 as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see
http://www.gnu.org/licenses/.

Trademarks used herein are the property of their respective owners. -->
<!-- Created by GNU Texinfo 6.3, http://www.gnu.org/software/texinfo/ -->
<head>
<title>Libraries for Vicare Scheme: amb examples coloring</title>

<meta name="description" content="Libraries for Vicare Scheme: amb examples coloring">
<meta name="keywords" content="Libraries for Vicare Scheme: amb examples coloring">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<link href="index.html#Top" rel="start" title="Top">
<link href="concept-index.html#concept-index" rel="index" title="concept index">
<link href="amb-examples.html#amb-examples" rel="up" title="amb examples">
<link href="simple_002dmatch.html#simple_002dmatch" rel="next" title="simple-match">
<link href="amb-examples-ssc.html#amb-examples-ssc" rel="prev" title="amb examples ssc">
<style type="text/css">
<!--
a.summary-letter {text-decoration: none}
blockquote.indentedblock {margin-right: 0em}
blockquote.smallindentedblock {margin-right: 0em; font-size: smaller}
blockquote.smallquotation {font-size: smaller}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
div.lisp {margin-left: 3.2em}
div.smalldisplay {margin-left: 3.2em}
div.smallexample {margin-left: 3.2em}
div.smalllisp {margin-left: 3.2em}
kbd {font-style: oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
pre.smalldisplay {font-family: inherit; font-size: smaller}
pre.smallexample {font-size: smaller}
pre.smallformat {font-family: inherit; font-size: smaller}
pre.smalllisp {font-size: smaller}
span.nolinebreak {white-space: nowrap}
span.roman {font-family: initial; font-weight: normal}
span.sansserif {font-family: sans-serif; font-weight: normal}
ul.no-bullet {list-style: none}
-->
</style>


</head>

<body lang="en">
<a name="amb-examples-coloring"></a>
<div class="header">
<p>
Previous: <a href="amb-examples-ssc.html#amb-examples-ssc" accesskey="p" rel="prev">amb examples ssc</a>, Up: <a href="amb-examples.html#amb-examples" accesskey="u" rel="up">amb examples</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<a name="Graph-coloring"></a>
<h4 class="subsubsection">1.4.2.3 Graph coloring</h4>


<p>The following program shows how to select colors to paint European
nations on map: given a map of nations we want to paint each nation with
a color different from the one of its neighbors; it is known that 4
colors are always enough (even though more than 4 colors are
aesthetically better).  The code is derived from an example in &ldquo;Teach
Yourself Scheme in Fixnum Days&rdquo; by Dorai Sitaram.
</p>
<p>We start with the prelude:
</p>
<div class="example">
<pre class="example">#!r6rs
(import (vicare)
  (vicare language-extensions amb)
  (only (vicare language-extensions syntaxes)
        define-values))
</pre></div>

<p>We represent the map with a graph: one node for each nation; nations
that face each other are connected by a link.  We select the adjacency
list representation.  We want to make sure that we have correctly
defined the graph: two nodes connected by a link must be present in the
adjacency list of each other.  A better graph definition syntax would
have integrated this check.
</p>
<div class="example">
<pre class="example">(define-record-type node
  (fields (immutable name)
          ;;List of NODE  records representing the adjacency
          ;;list of this node.
          (mutable neighbors)
          ;;Symbol representing this node's color.
          (mutable color))
  (protocol (lambda (maker)
              (lambda (name)
                (maker name '() #f)))))

(define-syntax define-nodes
  (syntax-rules ()
    ((_ ?nodes-var (?node (?neighbor ...)) ...)
     (begin
       (define ?node (make-node (quote ?node)))
       ...
       (module ()
         (node-neighbors-set! ?node (list ?neighbor ...))
         ...)
       (define ?nodes-var
         (list ?node ...))
       (module ()
         (assert-graph-consistency ?nodes-var))))
    ))

(define (assert-graph-consistency nodes)
  ;;Verify that every node is  present in the adjacency list
  ;;of all its neighbors.
  ;;
  (define who 'assert-graph-consistency)
  (for-each
      (lambda (node)
        (for-each
            (lambda (neighbor)
              (unless (memq node (node-neighbors neighbor))
                (assertion-violation who
                  &quot;incorrect node links&quot;
                  (node-name node)
                  (node-name neighbor))))
          (node-neighbors node)))
    nodes))
</pre></div>

<p>We are interested in nations that face each other, even when there is a
sea between them; for example: it is aesthetically more pleasant if
France and the United Kingdom are painted in different colors.
</p>
<div class="example">
<pre class="example">(define-nodes europe-facing-nations
  (portugal        (spain))
  (spain           (portugal andorra france))
  (andorra         (spain france))
  (france          (spain andorra monaco italy
                    switzerland germany luxembourg
                    belgium united-kingdom))
  (united-kingdom  (france belgium netherlands denmark
                    norway iceland ireland))
  (ireland         (united-kingdom iceland))
  (monaco          (france))
  (italy           (france greece albania montenegro
                    croatia slovenia austria switzerland
                    san-marino))
  (san-marino      (italy))
  (switzerland     (france italy austria germany
                    liechtenstein))
  (liechtenstein   (switzerland austria))
  (germany         (france switzerland austria czech-republic
                    poland sweden denmark netherlands
                    belgium luxembourg))
  (belgium         (france luxembourg germany netherlands
                    united-kingdom))
  (netherlands     (belgium germany united-kingdom))
  (luxembourg      (france germany belgium))
  (austria         (italy slovenia hungary slovakia
                    czech-republic germany switzerland
                    liechtenstein))
  (slovenia        (italy croatia hungary austria))
  (croatia         (italy montenegro bosnia serbia hungary
                    slovenia))
  (bosnia          (croatia montenegro serbia))
  (montenegro      (croatia italy albania serbia bosnia))
  (albania         (italy greece macedonia serbia
                    montenegro))
  (greece          (italy cyprus bulgaria macedonia albania))
  (cyprus          (greece))
  (macedonia       (albania greece bulgaria serbia))
  (bulgaria        (macedonia greece romania serbia))
  (serbia          (montenegro albania macedonia bulgaria
                    romania hungary croatia bosnia))
  (romania         (serbia bulgaria hungary))
  (hungary         (slovenia croatia serbia romania slovakia
                    austria))
  (slovakia        (austria hungary poland czech-republic))
  (czech-republic  (germany austria slovakia poland))
  (poland          (germany czech-republic slovakia sweden))
  (denmark         (united-kingdom germany sweden norway))
  (sweden          (norway denmark germany poland finland))
  (norway          (united-kingdom denmark sweden finland
                    iceland))
  (finland         (sweden norway))
  (iceland         (ireland united-kingdom norway)))
</pre></div>

<p>The only use of <code>amb</code> is in the function that selects a color for a
nation.  We use <code>amb-permute</code>, rather than the plain <code>amb</code>, to
try assigning a different first color to the nations (statistically
speaking).
</p>
<div class="example">
<pre class="example">(define (choose-color)
  ;;Every time we call this function: we start a new choice.
  ;;
  (amb-permute 'red 'yellow 'blue 'green))
</pre></div>

<p>We validate a possible solution with <code>amb-assert</code>, checking that a
node/nation has color different from the one of its neighbors:
</p>
<div class="example">
<pre class="example">(define (validate-all-nodes-color all-nodes)
  (for-all validate-single-node-color all-nodes))
</pre></div>

<p>To build a &ldquo;better&rdquo; starting point, we also want to test the color of
a single node against the colors of its neighbors while we are building
the initial choice.  When calling this function: the neighbors may not
have a color yet: their <code>color</code> field can be set to <code>#f</code>.
</p>
<div class="example">
<pre class="example">(define (validate-single-node-color node)
  (amb-assert (not (memq (node-color node)
                         (map node-color
                           (node-neighbors node))))))
</pre></div>

<p>The actual coloring function that selects a color for each nation and
validates the solution until all the constraints are satisfied:
</p>
<div class="example">
<pre class="example">(define (color-nations nations)
  (with-ambiguous-choices
   ;;Build an initial choice.
   (for-each
       (lambda (nation)
         (node-color-set! nation (choose-color))
         (validate-single-node-color nation))
     nations)
   ;;Validate the choice and backtrack if needed.
   (validate-all-nodes-color nations)))
</pre></div>

<p>We also need to display the result:
</p>
<div class="example">
<pre class="example">(define (print-colors nations)
  (for-each
      (lambda (nation)
        (print &quot;~a: ~a\n&quot;
               (node-name nation)
               (node-color nation))
        (for-each
            (lambda (neighbor)
              (print &quot;\t~a: ~a\n&quot;
                     (node-name neighbor)
                     (node-color neighbor)))
          (node-neighbors nation)))
    europe-facing-nations))

(define (print . args)
  (apply fprintf (current-error-port) args))
</pre></div>

<p>Run it:
</p>
<div class="example">
<pre class="example">(color-nations europe-facing-nations)
(print-colors  europe-facing-nations)
</pre></div>

<hr>
<div class="header">
<p>
Previous: <a href="amb-examples-ssc.html#amb-examples-ssc" accesskey="p" rel="prev">amb examples ssc</a>, Up: <a href="amb-examples.html#amb-examples" accesskey="u" rel="up">amb examples</a> &nbsp; [<a href="concept-index.html#concept-index" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
