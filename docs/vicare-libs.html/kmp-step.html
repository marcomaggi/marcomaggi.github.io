<html lang="en">
<head>
<title>kmp step - Libraries for Vicare Scheme</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Libraries for Vicare Scheme">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="kmp.html#kmp" title="kmp">
<link rel="prev" href="kmp-vector.html#kmp-vector" title="kmp vector">
<link rel="next" href="kmp-partial.html#kmp-partial" title="kmp partial">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This document describes the libraries distributed along with version
0.3d6 of Vicare Scheme, an R6RS compliant native
compiler for the Scheme language.

Copyright (C) 2010-2013 by Marco Maggi.

Copyright (C) 2006-2010 by Abdulaziz Ghuloum.

The documentation of IrRegex is Copyright (C) 2005-2012 Alex
Shinn.  All rights reserved.

The documentation of Pregexp is Copyright (C) 1999-2005 Dorai
Sitaram.  All rights reserved.

The documentation of the library `(vicare formations)' is derived
from the documentation of Guile.  Copyright (C) 1996-2005,
2009-2013 Free Software Foundation.

The documentation of the libraries `(vicare parser-tools silex
---)' are derived form the documentation of SILex.  Copyright
(C) 2001, 2009 Danny Dube'.

     This program is free software: you can redistribute it and/or
     modify it under the terms of the GNU General Public License
     version 3 as published by the Free Software Foundation.

     This program is distributed in the hope that it will be useful, but
     WITHOUT ANY WARRANTY; without even the implied warranty of
     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     General Public License for more details.

     You should have received a copy of the GNU General Public License
     along with this program.  If not, see
     `http://www.gnu.org/licenses/'.

Trademarks used herein are the property of their respective owners.-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="kmp-step"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="kmp-partial.html#kmp-partial">kmp partial</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="kmp-vector.html#kmp-vector">kmp vector</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="kmp.html#kmp">kmp</a>
<hr>
</div>

<h3 class="section">24.4 Single step of the search</h3>

<div class="defun">
&mdash; Function: <b>%kmp-step</b><var> item= item-ref restart-vector next-value-from-text next-index-in-pattern pattern pattern-start<a name="index-g_t_0025kmp_002dstep-4723"></a></var><br>
<blockquote><p>This function encapsulates the work performed by one step of the
<acronym>KMP</acronym> search.  Return the new index in the pattern; that is, how
much of the pattern we have matched, including the given value from
text. 
</p></blockquote></div>

   <p>Searching for the pattern <code>"hello"</code> in the text <code>"ciao hello
salut"</code> looks like this:

<pre class="example">     (let* ((text          "ciao hello salut")
            ;;              01234567890123456
            ;;              0         1
            (text-past     (string-length text))
     
            (pattern       "ciao")
            (pattern-start 0)
            (pattern-past  (string-length pattern))
     
            (rv            (%kmp-make-restart-vector char=? string-ref
                              pattern pattern-start pattern-past)))
       (let loop ((ti 0)
                  (pi pattern-start))
         (or (and (= pi pattern-past) ti) ; found
             (and (not (= ti text-past))  ; not found
                  (loop (+ 1 ti)
                        (%kmp-step char=? string-ref rv
                                   (string-ref text ti)
                                   pi pattern pattern-start))))))
     &rArr; 10
</pre>
   <p class="noindent">if the pattern was not found the return value is <code>#f</code>; if the
pattern was found the return value is the index in the text of the
character past the last matched one.

   <p>Abstracting the search from a string to a generic source of characters,
we can write the following function, whose return value is the same as
the loop above:

<pre class="example">     (define (return-match-past end-of-text? get-next-char
                                pattern pattern-start pattern-past)
       (let ((rv (%kmp-make-restart-vector char=? string-ref
                    pattern pattern-start pattern-past)))
         (let loop ((ti 0)
                    (pi pattern-start))
           (or (and (= pi pattern-past) ti) ; found
               (and (not (end-of-text?))    ; not found
                    (loop (+ 1 ti)
                          (%kmp-step char=? string-ref rv
                             (get-next-char)
                             pi pattern pattern-start)))))))
</pre>
   <p class="noindent">a usage example with a string looks like this:

<pre class="example">     (let* ((text           "ciao hello salut")
            ;;               01234567890123456
            ;;               0         1
            (pattern        "hello")
            (ti             0)
            (end-of-text?   (lambda ()
                              (= ti (string-length text))))
            (get-next-char  (lambda ()
                              (begin0
                                  (string-ref text ti)
                                (set! ti (+ 1 ti))))))
       (return-match-past end-of-text? get-next-char
                          pattern 0 (string-length pattern)))
     &rArr; 10
</pre>
   <p class="noindent">a usage example with an input port looks like this:

<pre class="example">     (let* ((text           "ciao hello salut")
            ;;               01234567890123456
            ;;               0         1
            (pattern        "salut")
            (port           (open-string-input-port text))
            (end-of-text?   (lambda ()
                              (eof-object? (peek-char port))))
            (get-next-char  (lambda ()
                              (read-char port))))
       (return-match-past end-of-text? get-next-char
                          pattern 0 (string-length pattern)))
     &rArr; 16
</pre>
   <!-- page -->
   </body></html>

