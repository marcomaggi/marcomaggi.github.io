<html lang="en">
<head>
<title>2015 May 26 - Marco's Weblog</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Marco's Weblog">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="2015.html#g_t2015" title="2015">
<link rel="prev" href="2015-May-26-bis.html#g_t2015-May-26-bis" title="2015 May 26 bis">
<link rel="next" href="2015-May-19.html#g_t2015-May-19" title="2015 May 19">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
Copyright (C) 2015 by Marco Maggi <marco.maggi-ipsu@poste.it>

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with Invariant Sections being ``GNU Free Documentation
     License'' and ``GNU General Public License'', no Front--Cover
     Texts, and no Back--Cover Texts.  A copy of the license is
     included in the section entitled ``GNU Free Documentation
     License''.
-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
<link rel="stylesheet" type="text/css" href="weblog.css">
</head>
<body>
<div class="node">
<a name="g_t2015-May-26"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="2015-May-19.html#g_t2015-May-19">2015 May 19</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="2015-May-26-bis.html#g_t2015-May-26-bis">2015 May 26 bis</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="2015.html#g_t2015">2015</a>
<hr>
</div>

<h3 class="unnumberedsec">Review of extensions for Vicare (part 1)</h3>

<div align="center">Posted on May 26, 2015</div>

   <p>I am doing a review of <a href="References.html#References">Vicare</a>'s extensions implementing interfaces to foreign
libraries.  Having formally abandoned Sourceforge and Gitorious, the code is on
<a href="https://github.com/marcomaggi/">Github</a> and
<a href="https://bitbucket.org/marcomaggi/">Bitbucket</a>.  Yes, I am procrastinating the
expander's code review I have started.

<!--  -->
<h4 class="unnumberedsubsec">Vicare/CRE2</h4>

<p>For some time Vicare's package has included an interface to the C language library
<a href="http://github.com/marcomaggi/cre2/">CRE2</a>, which in turn is a C wrapper for the
C++ language library <a href="https://github.com/google/re2/">re2</a>.  re2 was originally
hosted at Google Code, but it is now hosted at Github.

   <p>The purpose of including this interface in the main Vicare Scheme package was
twofold: to include a fast regular expressions engine; to test the viability of
inclusion of foreign library interfaces in the main package:

     <ul>
<li>Vicare Scheme now comes with multiple regular expressions engines (<code>(vicare
irregexp)</code>, <code>(vicare pregexp)</code>, <acronym>POSIX</acronym> regular expressions in the library
<code>(vicare glibc)</code>); I rarely use them, so I do not really feel the need to have
another engine there.

     <li>I have verified that it is possible without much hassle to include such interfaces in
the main package, thanks to the <acronym>GNU</acronym> Autotools infrastructure.  Fine. 
</ul>

   <p>So, I have extracted the interface to CRE2 from the main package and it has now its
own <a href="http://github.com/marcomaggi/vicare-cre2/">package</a>.  There is nothing new
about it; the <acronym>API</acronym> has not changed.

<!--  -->
<h4 class="unnumberedsubsec">Vicare/AO</h4>

<p>This is a new package interfacing <a href="http://xiph.org/ao/">Libao</a>: a cross&ndash;platform
audio library that allows programs to output audio using a simple <acronym>API</acronym> on a wide
variety of platforms (this description is straight from their web site).  I doubt it
can be used for serious stuff, but, anyway, it can be used to play sounds
programmatically generated by direct computation of the waveform; it can also
generate <code>wav</code> files.

   <p>Here is an example playing a chunk of Black Sabbath's &ldquo;Iron Man&rdquo; song:

<pre class="lisp">     (import (vicare)
       (prefix (vicare multimedia ao) ao.)
       (prefix (vicare multimedia ao constants) ao.)
       (vicare numerics constants))
     
     (ao.ao-initialise)
     
     (define-constant A4   440.00)
     (define-constant B4   493.88)
     (define-constant C5   523.25)
     (define-constant D5   587.33)
     (define-constant E5   659.25)
     (define-constant F5   698.46)
     (define-constant Fs5  739.995)
     (define-constant G5   783.99)
     (define-constant A5   880.00)
     (define-constant B5   987.77)
     (define-constant C6  1046.50)
     
     (define sample-format
       (let ((bits         16)    ;how many bits per sample
             (rate         44100) ;how many samples per second
             (channels     2)
             (byte-format  ao.AO_FMT_LITTLE)
             (matrix       "L,R"))
         (ao.make-ao-sample-format bits rate channels byte-format matrix)))
     
     (define device
       (let ((driver-options #f))
         (ao.ao-open-live (ao.ao-default-driver-id) sample-format driver-options)))
     
     (define (make-melody frm melody)
       ;;Build and return a samples bytevector, filled with a sinusoidal waveform.
       ;;
       (define number-of-ticks-per-second
         (infix inexact(ao.ao-sample-format-rate(frm))))
       (define number-of-ticks-per-millisecond
         (infix number-of-ticks-per-second / 1000.0))
       (define ticks*
         (map (lambda (note)
                (let ((msecs (cdr note)))
                  (infix exact(floor(msecs * number-of-ticks-per-millisecond)))))
           melody))
       (define samples.len
         (let ((total-number-of-ticks (fold-left + 0 ticks*)))
           (infix ao.ao-sample-format-bits(frm) / 8
                  * ao.ao-sample-format-channels(frm)
                  * total-number-of-ticks)))
       (receive-and-return (samples.bv)
           (make-bytevector samples.len)
         (fold-left
             (lambda (i.start note ticks)
               (let ((freq  (car note))
                     (msecs (cdr note)))
                 (receive-and-return (i.end)
                     (+ i.start ticks)
                   (fill-tone! samples.bv i.start i.end freq number-of-ticks-per-second))))
           0 melody ticks*)))
     
     (define (fill-tone! samples.bv i.start i.end freq number-of-ticks-per-second)
       ;;FREQ must be the tone frequency in Hertz.
       ;;
       (define A     (infix 0.75 * 32768.0))
       (define omega (infix 2 * greek-pi * freq))
       (define sample-max (exact (floor (* A 0.20))))
       (define sample-min (- sample-max))
       (do ((i i.start (fxadd1 i)))
           ((fx&gt;=? i i.end))
         (let* ((time   (infix inexact(i / number-of-ticks-per-second)))
                (sample (exact (floor (infix A * sin(omega * time)))))
                (j      (infix 4 * i)))
           ;;Put the same stuff in left and  right channels.  The bytevector is an array
           ;;of 32-bit slots, each with format:
           ;;
           ;;    channel 1 LSB channel 2 LSB channel 1 MSB channel 2 MSB
           ;;   |-------------|-------------|-------------|-------------|
           ;;
           ;;where  LSB stands  for  Least  Significant Byte  and  MSB  stands for  Most
           ;;Significant Byte.
           ;;
           (let ((sample.lsb (infix #xFF &amp; sample)))
             (bytevector-u8-set! samples.bv j       sample.lsb)
             (bytevector-u8-set! samples.bv (+ j 2) sample.lsb))
           (let ((sample.msb (infix #xFF &amp; (sample &gt;&gt; 8))))
             (bytevector-u8-set! samples.bv (+ j 1) sample.msb)
             (bytevector-u8-set! samples.bv (+ j 3) sample.msb)))))
     
     (define iron-man
       (let ((T 1500)
             (pause '(0 . 100))
             (pause/2 '(0 . 50)))
         (make-melody sample-format
                      `((,B4  . ,(* T 1/4)) ,pause
                        (,D5  . ,(* T 1/4)) ,pause
                        (,D5  . ,(* T 1/8)) ,pause
                        (,E5  . ,(* T 1/8)) ,pause
                        (,E5  . ,(* T 1/4)) ,pause
                        (,G5  . ,(* T 1/16)) ,pause/2
                        (,Fs5 . ,(* T 1/16)) ,pause/2
                        (,G5  . ,(* T 1/16)) ,pause/2
                        (,Fs5 . ,(* T 1/16)) ,pause/2
                        (,G5  . ,(* T 1/16)) ,pause/2
                        (,Fs5 . ,(* T 1/16)) ,pause/2
                        (,D5  . ,(* T 1/8)) ,pause
                        (,D5  . ,(* T 1/8)) ,pause
                        (,E5  . ,(* T 1/8)) ,pause
                        (,E5  . ,(* T 1/4))
                        ))))
     
     (ao.ao-play device iron-man)
     (ao.ao-close device)
</pre>
   <p>Libao's documentation is very dry.  In future I should dig in the code and document
what is the expected format of audio samples, it is not really clear to me.

<!--  -->
<h4 class="unnumberedsubsec">Vicare/SQLite</h4>

<p>I have revamped the <a href="http://github.com/marcomaggi/vicare-sqlite/">interface</a> to
<a href="http://www.sqlite.org/">SQLite</a>, the database engine.  The <code>master</code> branch
has a tag <code>begin-support-for-vicare-scheme-0.4</code> after which Vicare Scheme
version <code>0.4</code> must be used.  I did a review of the code.  The current head of
Vicare/SQLite's <code>master</code> branch should work with the current head of Vicare
Scheme's <code>master</code> branch.

<!--  -->
<h4 class="unnumberedsubsec">On dependencies between libraries</h4>

<p>The current scenario is that: there is a tight dependence between the boot image and
compiled libraries.  First we have to compile the boot image, then we have to
hierarchically compile the libraries.  If we recompile a library: we have to
recompile all the libraries that depend on it.  If we recompile the boot image: we
have to recompile everything.  I hate this.

   <p>This was less a problem before, when the scenario was: we install source libraries
and automatically compile them in the <acronym>FASL</acronym> files cache.

   <p>I want to do something about it, so that the scenario is more like the shared
objects's hell of C language libraries.  &ldquo;Doing something&rdquo; means to implement
tables of storage location gensyms referencing exported syntactic bindings.

<!-- page -->
   </body></html>

