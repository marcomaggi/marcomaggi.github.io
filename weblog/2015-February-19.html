<html lang="en">
<head>
<title>2015 February 19 - Marco's Weblog</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Marco's Weblog">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="2015.html#g_t2015" title="2015">
<link rel="prev" href="2015-February-19-bis.html#g_t2015-February-19-bis" title="2015 February 19 bis">
<link rel="next" href="2015-February-18.html#g_t2015-February-18" title="2015 February 18">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
Copyright (C) 2015 by Marco Maggi <marco.maggi-ipsu@poste.it>

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Free Documentation License,
     Version 1.3 or any later version published by the Free Software
     Foundation; with Invariant Sections being ``GNU Free Documentation
     License'' and ``GNU General Public License'', no Front--Cover
     Texts, and no Back--Cover Texts.  A copy of the license is
     included in the section entitled ``GNU Free Documentation
     License''.
-->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
<link rel="stylesheet" type="text/css" href="weblog.css">
</head>
<body>
<div class="node">
<a name="g_t2015-February-19"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="2015-February-18.html#g_t2015-February-18">2015 February 18</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="2015-February-19-bis.html#g_t2015-February-19-bis">2015 February 19 bis</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="2015.html#g_t2015">2015</a>
<hr>
</div>

<h3 class="unnumberedsec">On running parallel processes</h3>

<p>At present (Feb 2015), <a href="References.html#References">Vicare</a> supports <em>concurrency</em>, which is different
from <em>parallelism</em>.  This section is about what's currently in the <a href="References.html#References">master</a>
branch, not the latest Vicare release.

   <p>I have recently integrated the library <code>(vicare language-extensions
coroutines)</code> in the boot image: the code is small and does something that I find
useful.  Coroutines implemented on top of continuations provide concurrency.

   <p>Vicare does <strong>not</strong> implement multithreading; meaning <em>native</em>
multithreading, as in using the <code>pthreads</code> library.  It should be possible to
implement it, but it is not trivial to interface threads with the garbage collector;
some significant work must be put in.  I would love to have native multithreading in
Vicare.  Multithreading support would mean supporting parallelism.

   <p>The only way to have some actual parallelism is to fork child processes; it is not
for everything, because forking a <samp><span class="command">vicare</span></samp> process costs resources.

<!--  -->
<h4 class="unnumberedsubsec">Some example libraries</h4>

<p>Vicare now comes with some libraries that run &ldquo;well known&rdquo; external programs:

<pre class="example">     (vicare posix sendmail)
     (vicare posix mailx)
     (vicare posix curl)
     (vicare posix wget)
     (vicare posix find)
</pre>
   <p class="noindent">they are really minimal: they just show how to run an external program by calling one
of the <code>fork</code> variants then <code>execvp</code> from <code>(vicare posix)</code>; their
source code can be taken as starting point for more flexible interfaces.  The
following demo program sends an email message with <samp><span class="command">sendmail</span></samp> and prints its
output:

<pre class="lisp">     #!r6rs
     (import (vicare)
       (only (vicare posix sendmail)
             sendmail))
     
     (define (send-it message)
       (define rv
         (sendmail (string-&gt;ascii message)))
       (display rv)
       (newline)
       (flush-output-port (current-output-port)))
     
     (send-it "From: &lt;marco@localhost&gt;
     To: &lt;marco@localhost&gt;
     Subject: demo 01
     MIME-Version: 1.0
     Content-Type: text/plain
     
     This is demo 01.
     --\x20;
     Marco Maggi
     ")
</pre>
   <p>The <acronym>API</acronym> of these libraries is &ldquo;blocking&rdquo;: we run a process and wait for its
termination with <code>waitpid</code> from <code>(vicare posix)</code>.  These library do not
run a <em>parallel</em> process: they run a <em>synchronous</em> process.

<!--  -->
<h4 class="unnumberedsubsec">Forking the current process</h4>

<p>To run a parallel process we first have to fork the current process into
<em>parent</em> and <em>child</em> processes; if we actually want another
<samp><span class="command">vicare</span></samp> process we are fine with the child, otherwise we have to replace the
child process with a process running an external program.

   <p>On <acronym>POSIX</acronym> platforms, right after a <code>fork</code> call: the parent and child will
share the standard input/output file descriptors <code>0</code>, <code>1</code>, <code>2</code>.  A
very common task when forking a process is to replace the standard file descriptors
in the child process with other file descriptors that are connected to pipes, so that
the parent and the child can communicate through them.

   <p>With the raw <code>fork</code> function from <code>(vicare posix)</code> we can do this with:

<pre class="lisp">     (import (vicare)
       (prefix (vicare posix) px.))
     
     (let-values
         (((child-stdin          parent-&gt;child-stdin) (px.pipe))
          ((parent&lt;-child-stdout child-stdout)        (px.pipe))
          ((parent&lt;-child-stderr child-stderr)        (px.pipe)))
       (px.fork
         (lambda (child-pid)
           ;;Here we are in the parent.
           (px.close child-stdin)
           (px.close child-stdout)
           (px.close child-stderr)
           (with-unwind-protection
               (lambda (why)
                 (px.close parent-&gt;child-stdin)
                 (px.close parent&lt;-child-stdout)
                 (px.close parent&lt;-child-stderr))
             (lambda ()
               (do-that-thing))))
         (lambda ()
           ;;Here we are in the child.
           (guard (E (else
                      (print-condition E)
                      (exit 1)))
             (px.close parent-&gt;child-stdin)
             (px.close parent&lt;-child-stdout)
             (px.close parent&lt;-child-stderr)
             (begin        ;setup stdin
               (px.close 0)
               (px.dup2  child-stdin 0)
               (px.close child-stdin))
             (begin        ;setup stdout
               (px.close 1)
               (px.dup2  child-stdout 1)
               (px.close child-stdout))
             (begin        ;setup stderr
               (px.close 2)
               (px.dup2  child-stderr 2)
               (px.close child-stderr)))
             (do-this-thing))))
</pre>
   <p class="noindent">with this code the parent process can:

     <ul>
<li>Write in the child's <code>stdin</code> file descriptor by writing to
<code>parent-&gt;child-stdin</code>.

     <li>Read from the child's <code>stdout</code> file descriptor by reading from
<code>parent&lt;-child-stdout</code>.

     <li>Read from the child's <code>stderr</code> file descriptor by reading from
<code>parent&lt;-child-stderr</code>. 
</ul>

<p class="noindent">and the child process can:

     <ul>
<li>Read from its own <code>stdin</code> file descriptor by reading from <code>0</code>.

     <li>Write into its own <code>stdout</code> file descriptor by writing to <code>1</code>.

     <li>Write into its own <code>stderr</code> file descriptor by writing to <code>2</code>. 
</ul>

   <p>The library <code>(vicare posix)</code> has some functions to automate this process and
wrap the raw file descriptors with proper Scheme input/output port objects.  We need
to remember that:

     <ul>
<li>At the <acronym>POSIX</acronym> <acronym>API</acronym> level: we have the standard file descriptors <code>0</code>,
<code>1</code>, <code>2</code>.

     <li>At the Vicare <acronym>API</acronym> level:

          <ul>
<li>We have the binary ports returned by <code>(standard-input-port)</code>,
<code>(standard-output-port)</code>, <code>(standard-error-port)</code>.

          <li>We have the textual  ports returned by <code>(console-input-port)</code>,
<code>(console-output-port)</code>, <code>(console-error-port)</code>. 
</ul>
     </ul>

   <p>Let's see some usage examples for the <code>fork</code> variants:

     <dl>
<dt><code>fork-with-fds</code><dd>The following example shows how to fork a process, setup the file descriptors and
wait for child to terminate.  The function <code>fork-with-fds</code> takes care of setting
up the file descriptors in both the parent and child.

     <pre class="lisp">          #!vicare
          (import (vicare)
            (prefix (vicare posix) px.))
          
          (define (parent-proc child-pid parent-&gt;child-stdin parent&lt;-child-stdout parent&lt;-child-stderr)
            (unwind-protect
                (let ((bufout (make-bytevector 4 0))
                      (buferr (make-bytevector 4 0)))
                  (px.write parent-&gt;child-stdin '#ve(ascii "ciao\n"))
                  (let ((status (px.waitpid child-pid 0)))
                    (if (and (px.WIFEXITED status)
                             (zero? (px.WEXITSTATUS status)))
                        (begin
                          (px.read parent&lt;-child-stdout bufout)
                          (px.read parent&lt;-child-stderr buferr)
                          (values bufout buferr))
                      (error #f
                        "child process exited abnormally"
                        status))))
              (px.close parent-&gt;child-stdin)
              (px.close parent&lt;-child-stdout)
              (px.close parent&lt;-child-stderr)))
          
          (define (child-thunk)
            (guard (E (else
                       (print-condition E)
                       (exit 1)))
              (let ((buf (make-bytevector 5 0)))
                (px.read 0 buf)
                (assert (bytevector=? buf '#ve(ascii "ciao\n")))
                (px.write 1 '#ve(ascii "out\n"))
                (px.write 2 '#ve(ascii "err\n"))
                (exit 0))))
          
          (px.fork-with-fds parent-proc child-thunk)
          &rArr; #ve(ascii "out\n") #ve(ascii "err\n")
</pre>
     <br><dt><code>fork-with-binary-ports</code><dd>The following example shows how to fork a process, setup the file descriptors and
wrap them into binary ports, wait for child to terminate.  The function
<code>fork-with-binary-ports</code> takes care of setting up the file descriptors in both
the parent and child and wrapping them into binary ports.

     <pre class="lisp">          #!vicare
          (import (vicare)
            (prefix (vicare posix) px.))
          
          (define (parent-proc child-pid child-stdin child-stdout child-stderr)
            (unwind-protect
                (let ((bufout (make-bytevector 4 0))
                      (buferr (make-bytevector 4 0)))
                  (put-bytevector child-stdin '#ve(ascii "ciao\n"))
                  (flush-output-port child-stdin)
                  (let ((status (px.waitpid child-pid 0)))
                    (if (and (px.WIFEXITED status)
                             (zero? (px.WEXITSTATUS status)))
                        (begin
                          (get-bytevector-n! child-stdout bufout 0 4)
                          (get-bytevector-n! child-stderr buferr 0 4)
                          (values bufout buferr))
                      (error #f
                        "child process exited abnormally"
                        status))))
              (close-output-port child-stdin)
              (close-input-port  child-stdout)
              (close-input-port  child-stderr)))
          
          (define (child-thunk)
            (guard (E (else
                       (print-condition E)
                       (exit 1)))
              (define-constant stdin-port
                (standard-input-port))
              (define-constant stdout-port
                (standard-output-port))
              (define-constant stderr-port
                (standard-error-port))
              (let ((buf (make-bytevector 5 0)))
                (get-bytevector-n! stdin-port buf 0 5)
                (assert (bytevector=? buf '#ve(ascii "ciao\n")))
                (put-bytevector stdout-port '#ve(ascii "out\n"))
                (put-bytevector stderr-port '#ve(ascii "err\n"))
                (flush-output-port stdout-port)
                (flush-output-port stderr-port)
                (exit 0))))
          
          (px.fork-with-binary-ports parent-proc child-thunk)
          &rArr; #ve(ascii "out\n") #ve(ascii "err\n")
</pre>
     <br><dt><code>fork-with-textual-ports</code><dd>The following example shows how to fork a process, setup the file descriptors and
wrap them into textual ports, wait for child to terminate.  The function
<code>fork-with-textual-ports</code> takes care of setting up the file descriptors in both
the parent and child and wrapping them into textual ports.

     <pre class="lisp">          #!vicare
          (import (vicare)
            (prefix (vicare posix) px.))
          
          (define (parent-proc child-pid child-stdin child-stdout child-stderr)
            (unwind-protect
                (let ((bufout (make-string 4 #\x00))
                      (buferr (make-string 4 #\x00)))
                  (put-string child-stdin "ciao\n")
                  (flush-output-port child-stdin)
                  (let ((status (px.waitpid child-pid 0)))
                    (if (and (px.WIFEXITED status)
                             (zero? (px.WEXITSTATUS status)))
                        (begin
                          (get-string-n! child-stdout bufout 0 4)
                          (get-string-n! child-stderr buferr 0 4)
                          (values bufout buferr))
                      (error #f
                        "child process exited abnormally"
                        status))))
              (close-output-port child-stdin)
              (close-input-port  child-stdout)
              (close-input-port  child-stderr)))
          
          (define (child-thunk)
            (guard (E (else
                       (print-condition E)
                       (exit 1)))
              (let ((buf (make-string 5 #\x00)))
                (get-string-n! (console-input-port) buf 0 5)
                (assert (string=? buf "ciao\n"))
                (put-string (console-output-port) "out\n")
                (put-string (console-error-port) "err\n")
                (flush-output-port (console-output-port))
                (flush-output-port (console-error-port))
                (exit 0))))
          
          (px.fork-with-textual-ports parent-proc child-thunk)
          &rArr; "out\n" "err\n"
</pre>
     </dl>

<!--  -->
<h4 class="unnumberedsubsec">Managing <code>fork</code> and port sharing</h4>

<p>On <acronym>POSIX</acronym> platforms, right after a <code>fork</code> call: the parent and child will
share all the input/output file descriptors that are open at the time of the
<code>fork</code> call.  This is problematic.

   <p>Let's see what happens with the following program in which we open a file, fork the
process then close the file in both the parent and child:

<pre class="lisp">     #!vicare
     (import (vicare)
       (prefix (vicare posix) px.))
     
     (define (parent-proc child-pid)
       (px.waitpid child-pid 0)
       (close-output-port port)
       (exit 0))
     
     (define (child-thunk)
       (close-output-port port)
       (exit 0))
     
     (define port
       (parametrise ((output-file-buffer-size 8))
         (open-file-output-port "demo.txt"
     			   (file-options no-fail)
     			   (buffer-mode block)
     			   (native-transcoder))))
     
     (put-string port "0123456789ABCDEF")
     
     (px.fork parent-proc child-thunk)
</pre>
   <p class="noindent">the output <var>port</var> has a buffer of <code>8</code> characters and the string written to
it is <code>16</code> characters long; the call to <code>put-string</code> immediately flushes
the first <code>8</code> characters and leaves the rest in the output buffer.  The
<code>fork</code> call causes the output buffer and its contents to be duplicated, so, when
we close the port, the two buffers are flushed to the same file.  The resulting file
contents are:

<pre class="example">     0123456789ABCDEF89ABCDEF
</pre>
   <p class="noindent">which is not what we want.

   <p><acronym>POSIX</acronym> has a partial solution for this problem: to configure file descriptors as
<em>close&ndash;on&ndash;exec</em>; Vicare extends this to propose a partial solution: to
configure ports as <em>close&ndash;on&ndash;exec</em>.  Notice that it is
close&ndash;on&ndash;<strong>exec</strong>, not close&ndash;on&ndash;<strong>fork</strong>.  So we can try to address
this problem by:

     <ol type=1 start=1>
<li>Registering ports to be in close&ndash;on&ndash;exec mode.

     <li>Before calling <code>fork</code>: flush the ports in close&ndash;on&ndash;exec mode.

     <li>In the child process: close the ports in close&ndash;on&ndash;exec mode, to free allocated file
descriptors.
        </ol>

   <p>Here is the example above modified to do so:

<pre class="lisp">     #!vicare
     (import (vicare)
       (prefix (vicare posix) px.))
     
     (define (parent-proc child-pid)
       (px.waitpid child-pid 0)
       (close-output-port port)
       (exit 0))
     
     (define (child-thunk)
       (px.close-ports-in-close-on-exec-mode)
       (exit 0))
     
     (define port
       (parametrise ((output-file-buffer-size 8))
         (open-file-output-port "demo.txt"
                                (file-options no-fail)
                                (buffer-mode block)
                                (native-transcoder))))
     
     (px.port-set-close-on-exec-mode! port)
     
     (put-string port "0123456789ABCDEF")
     
     (px.flush-ports-in-close-on-exec-mode)
     (px.fork parent-proc child-thunk)
</pre>
   <p>Vicare also attempts to address the problem of &ldquo;error while flushing a port&rdquo;
(see <a href="2015-February-14.html#g_t2015-February-14">2015 February 14</a>).

<!--  -->
<h4 class="unnumberedsubsec">Asynchronously waiting for child process termination</h4>

<p>We are almost there: all is left to do is to understand how <strong>not</strong> to block
while waiting for the child process termination.  <code>(vicare posix)</code> provides the
<code>waitpid</code> function; the development code in the <code>master</code> branch has been
updated to allow proper interfacing with the underlying <code>waitpid()</code> <acronym>POSIX</acronym>
function when the option <code>WNOHANG</code> is used.

   <p>What we need to do is call <code>waitpid</code> as follows:

<pre class="lisp">     (waitpid child-pid WNOHANG)
</pre>
   <p class="noindent">and check the return value, which is special when <code>WNOHANG</code> is used: if the
return value is <code>#f</code>, the child process is still running; if the return value is
non&ndash;<code>#f</code>, then it is a fixnum representing the child process termination status.

   <p>Here is an example of waiting asynchronously:

<pre class="lisp">     #!vicare
     (import (vicare)
       (prefix (vicare posix) px.)
       (prefix (vicare platform constants) const.))
     
     (define (parent-proc child-pid)
       (define (print obj)
         (display obj)
         (flush-output-port))
       (let loop ((status (px.waitpid child-pid const.WNOHANG)))
         (cond ((not status)
                ;;Child still running.
                (print "not done yet\n")
                (px.nanosleep 0 #e1e8)
                (loop (px.waitpid child-pid const.WNOHANG)))
               ((px.WIFEXITED status)
                ;;Child exited.
                (print "done\n"))
               (else
                (error #f "child process exited abnormally" status)))))
     
     (define (child-thunk)
       (guard (E (else
                  (print-condition E)
                  (exit 1)))
         (px.nanosleep 1 0)
         (exit 0)))
     
     (px.fork parent-proc child-thunk)
</pre>
   <!--  -->
<h4 class="unnumberedsubsec">Running a parallel process</h4>

<p>At last!  Let's put it all together.  While we are at it: we fork the process inside
a coroutine, so that we can <code>(yield)</code> control while waiting for the child
process to terminate.  The following demo program starts a <samp><span class="command">vicare</span></samp> child
process and reads from it some Scheme objects through a textual port.

<pre class="lisp">     #!vicare
     (import (vicare)
       (prefix (vicare posix) px.)
       (prefix (vicare platform constants) const.))
     
     (define (parent-proc child-pid child-stdin child-stdout child-stderr)
       (with-unwind-protection
           (lambda (why)
             (close-output-port child-stdin)
             (close-input-port  child-stdout)
             (close-input-port  child-stderr))
         (lambda ()
           (let loop ((status (px.waitpid child-pid const.WNOHANG)))
             (cond ((not status)
                    ;;Child still running.
                    (yield)
                    (loop (px.waitpid child-pid const.WNOHANG)))
                   ((px.WIFEXITED status)
                    ;;Child exited.
                    (values status (read-all child-stdout) (get-string-all child-stderr)))
                   (else
                    (error #f "child process exited abnormally" status)))))))
     
     (define (child-thunk)
       (guard (E (else
                  (print-condition E)
                  (exit 1)))
         (px.close-ports-in-close-on-exec-mode)
         (write '(1 2 3) (console-output-port))
         (write '(4 5 6) (console-output-port))
         (flush-output-port (console-output-port))
         (put-string (console-error-port) "done\n")
         (flush-output-port (console-error-port))
         (exit 0)))
     
     (define (read-all port)
       (let ((obj (read port)))
         (if (eof-object? obj)
             '()
           (cons obj (read-all port)))))
     
     (coroutine
         (lambda ()
           (px.flush-ports-in-close-on-exec-mode)
           (receive (status out err)
               (px.fork-with-textual-ports parent-proc child-thunk)
             (printf "out: ~s\n" out)
             (printf "err: ~a\n" err)
             (flush-output-port (current-output-port)))))
     
     (finish-coroutines)
</pre>
   <!--  -->
<h4 class="unnumberedsubsec">Conclusions</h4>

<p>There are multiple things to do; there are decisions to be taken.

     <ul>
<li>Waiting for the child process termination can happen, at least, with 2 mechanisms:
coroutines, the event loop provided by the library <code>(vicare posix
simple-event-loop)</code>.

     <li>Exchanging data between the parent and the child can happen through multiple
mechanisms: connected file descriptors, connected binary ports, connected textual
ports, shared memory.

     <li>The exchanged data can be raw binary data or Scheme objects serialised in
<acronym>FASL</acronym> format or as textual symbolic expressions. 
</ul>

   <p>The existence of these possibilities is the reason why, at present, there is no
encapsulating <acronym>API</acronym> that implements starting a parallel process with a one&ndash;liner. 
Maybe, in some future, a pattern will emerge.

<!-- page -->
   </body></html>

